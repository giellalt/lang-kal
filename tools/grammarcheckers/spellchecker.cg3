#zsh -l -c '~/langtech/kal/tools/grammarcheckers/modes/kal-before-spell.mode'
#zsh -l -c '~/langtech/kal/tools/grammarcheckers/modes/kal-before-gram.mode

# An alternative to grammarchecker.cg3 that just does spelling.


INCLUDE STATIC liststemplates.cg3 ;

BEFORE-SECTIONS

### REMOVE ILLEGAL MORPHEME COMBINATIONS ###
# EMERGENCY SOLUTION until a better spellchecker generator is available


## Numerals
REMOVE (<W:2[5-9].*>r) OR (<W:3[0-9].*>r) (0 Num) ; 
#100-jut, 20't, 200-ut,
REMOVE Num + U + Int + 2Sg ; 
#200-uit
REMOVE OBLIQUE OR 3PlPoss OR U + Int (0 ("<\\d+\\u002Dut>"r) + <spelled>) ; 
#300-ut

## Stem specific: alphabetical
# A
REMOVE (/"aa"\ Gram/IV\ NIQ\ Der/vn\ ARAQ/l) ;
REMOVE (/"aa[kq]"\ LAAQ\ Der/nn/l) ;
REMOVE (/"\(.*\)"\ ARAQ/l) + ("aaneq") | ("aassik") ;
REMOVE (/"amer"\ Gram/TV\ RIAR\ Der/vv\ Gram/TV\ TAQ\ Der/vn/l) ;
REMOVE ("amar") + TAQ ;
# G
REMOVE GI (0 ("isaar")) ; #isaarilillu
# I
REMOVE (/"\(.*\)"\ \(.*\)\ Der/nv/l) + SI | LIR | GE | TU (0 ("ilaqutaq")) ;
#misspelling: ilaqutariinermut -> ilaqutariinnermut
REMOVE  (/"\ [LV]IK\ Der/nn\ LIUTE\ Der/nv/l) (0 ("illuatungeq")) ;
REMOVE (/"illuatungeq"\ KUQ\ Der/nn\ LIR\ Der/nv/l) ;
REMOVE (/"illuatungeq"\ LIUR\ Der/nv/l) ;
REMOVE (/"[iu]m[ei][pr]"\ Gram/[IT]V\ \(RI\)?\([TS]\)?\(A\)?\(I\)?R\ Der/vv\ \(Gram/TV\ \)?TAQ\ Der/vn/l) (0 ("<[Aa]me.*>"r ?)) ; #when mispelled: Amerikkap
REMOVE (/"ima\(q\)?"\ \(KUQ|LIK\)\ Der/nn/l) + (/[12]SgPoss/r) ;
REMOVE (/"ima\(q\)?"\ LUP\ Der/nv\ Gram/IV\ Gram/IV\ NIQ\ Der/vn/l) + 2SgPoss ;
REMOVE ("inalu\(k\)?"r) + (/[12]SgPoss/r) ;
REMOVE (/"isa\(p\)?"\ Gram/IV\ UMI\ Der/vv/l) ;
# K
REMOVE (/"krans"\ ARAQ\ Der/nn/l) (0 ("Hans.*"r)) ;
# L
REMOVE (/"lama"\ R/l) ;
# U
REMOVE (/"\(u\)?aa"\ Gram/IV\ NIQ\ Der/vn\ ARAQ\ Der/nn/l) ;
#misspelling: Haanseeqqap - aaneeraq, uaaneeraq
# S
REMOVE (/"saa"\ Orth/Arch \Gram/TV\ Gram/Pass\ NIQ/l) ;
REMOVE (/"saani\(k\)?"\ ARAQ/l) ;
REMOVE (/"saap"\ Gram/IV\ Gram/IV\ NIQ\ Der/vn\ ARAQ/l) ;
# T
REMOVE (/"taa\(p\)?"\ \(Gram/IV\ \)?Gram/IV\ NIQ\ Der/vn\ ARAQ/l) ;
REMOVE (/"time"\ Sem/Unit\ Err/Sub\ RSUR\ Der/nv\ Gram/[IT]V\ TAR\ Der/vv/l) ;

## Suffix specific
# A
REMOVE (/"\ ALUK\ Der/nn/l) + (/"\(ima|imeq\)"/l) ; #imialuk, imaaluk
# I
REMOVE (/"\(.*\)"\ Gram/IV\ IT\ Der/vv/l) (0 ("[a|n|s]ala"r)) ; 
#misspelling: Kalalalit -> Kalaallit aalaallit, nalaallit
#REMOVE (/"aala"\ Gram/IV\ IT\ Der/vv/l) ;
# K
REMOVE (/KUQ\ Der/nn\ LIUTE\ Der/nv/l) ;
#illuatungikuliuttut
# L
REMOVE (/"\(.*\)"\ \(LAAQ\ Der/nn\ \)?LIK\ Der/nn/l) + ("aalaq") | ("alaaq") | ("alla") | ("kalak") | ("malaq") ; 
# misspelling: kalalalit -> kalaallit
REMOVE (/LIK\ Der/nn\ U\ Der/nv\ Gram/IV\ VIP\ Der/vv/l) ;
#illuatungiliuvittut
REMOVE (/LIR\ Der/vv\ Gram/IV\ USIQ\ Der/vn/l) ;
#illuatungiuliutsit
REMOVE (/LIR\ Der/nv\ Gram/TV\ UTE\ Der/vv/l) + ("illuatungeq") ;
#illuatungiliuttut
REMOVE LIUTE (0 ("illuatungeq")) (0 ("illuatunge")) ;
REMOVE (/"\(.*\)" LUP Der/nv/l) + ("qalak") ;
REMOVE (/LUP\ Der/nv\ Gram/IV\ Gram/IV\ NI\(A\)?Q\ Der/vn\ \(UTE\ Der/nn\)?/l) ;
#misspelling: imalunniit
#imalunniut
#imalunniat
REMOVE (/LUP\ Der/nv\ Gram/IV\ Gram/IV\ NIUTE\ Der/vn/l) ;
#imalunniut
# M
REMOVE ("mamaq") + 2PlPoss ; # mamarsimi
# N
REMOVE (/Gram/\(Refl\)?\(Pass\)?\(TV\)?\ NIQ\ Der/vn\ \(Gram/TransverbNIQ\ \)?ARAQ/l) ; 
#misspelling: Haanseeqqap
REMOVE (/Orth/Arch\ Gram/IV\ NIQ\ Der/vn/l) ;
# S
REMOVE (/\ [A|U]\ Der/[vn]v\ Gram/IV\ SII\ Der/vv\ Gram/Refl\ V\ Opt/l) ;
REMOVE (/ SINNAA\ Der/vv\ Gram/TV\ Gram/Refl\ TUQ\ Der/vn/l) ;
# U
REMOVE (/U\ Der/nv\ Gram/IV\ LIR\ Der/vv\ VIP\ Der/vv/l) + ("illuatungeq") ;
REMOVE (/U\ Der/nv\ Gram/IV\ NIUTE\ Der/vv/l) (0 ("illuatungeq")) ;
#illuatungiuniuttut
REMOVE (/U\ Der/nv\ Gram/IV\ TUUQ\ Der/vn/l) ;
#illuatungiutuuttut
REMOVE (/Orth/Arch\ UTE\ Der/nn/l) ;
REMOVE (/UTE\ Der/nn\ LIR\ Der/nv\ Gram/TV\ UTE\ Der/vv/l) ;
#illuatungiusiuttut
REMOVE (/UTE\ Der/nn\ LIUTE\ Der/nv/l) ;
#illuatungiusiuttut
# V
REMOVE (/[LV]IK\ Der/nn\ LIR\ Der/nv\ Gram/TV\ UTE\ Der/vv/l) ; 
#illuatungiviliuttut

#NIQ+QAR vs NIQAR
REMOVE (/Gram/TV\ Gram/Refl\ NIQ\ Der/vn\ QAR\ Der/nv/l) ;
REMOVE (/QQU\ Der/vv\ Gram/TV\ Gram/Pass\ NIQ\ Der/vn\ QAR\ Der/nv/l) ;
REMOVE (/TIP\ Der/vv\ Gram/TV\ Gram/Refl\ NIQ Der/vn\ QAR\ Der/nv/l) ;
REMOVE (/TIP\ Der/vv\ Gram/TV\ Gram/TV\ Gram/Pass\ NIQ\ Der/vn\ QAR Der/nv/l) ;

## Case
REMOVE OBLIQUE - Trm (0 ("<.*[mn]ut>"r)) ; #remove suggestions with other case forms other than the case indicated by input

### Orth/Arch ###
### in the lexicon, but possible spelling errors [eg. archaic forms]
ADD &typo Orth/Arch (0 (/"arnap"\ Orth/Arch\ Gram/IV\ PALUP\ Der/vv\ Gram/IV\ TUQ\ Der/vn\ N/l)) ;
COPY ("arnaq" PALUP Der/nv Gram/IV Gram/IV TUQ Der/vn N &SUGGEST) EXCEPT ("arnap" Orth/Arch Gram/IV PALUP Der/vv Gram/IV TUQ Der/vn N &typo) BEFORE KASUS TARGET Orth/Arch (0 ("arnap" Orth/Arch Gram/IV PALUP Der/vv Gram/IV TUQ Der/vn N &typo)) ;
#arnappaluttumik


REMOVE V + Gram/Exclm ;
#REMOVE (<W:3.*>r) + N OR V ;
REMOVE (/"\(.*\)"S/r) - NIQ (0 (VSTR:"$1"S) + NIQ) ;
#misspelling: Haanseeqqap, xxx+NIQ vs xxxneq. no reason to have identical forms
REMOVE (".*i"r) + Prop (0 consprop) ;

SECTION

### possible suffix errors, error corrections offered even though the form is in the lexicon

#a
#attitut->atertut
ADD &typo ("<attitut>") ;
COPY ("ateq" N Aeq Sg &SUGGEST) EXCEPT ("ate" N Aeq Pl &typo) TARGET ("<attitut>") + &typo ;
#Taamaattumik Naani atissatut qinnutigerusukkatsigu attitut akuerisassatut qinnutigaarput.

#atuinnaqqunaaq->atuinnaqqunaqaaq
ADD &typo ("<atuinnaqqunaaq>") ;
COPY ("ator" Gram/IV INNAR Der/vv QQU Der/vv Gram/TV NAR Der/vv Gram/IV QE Der/vv Gram/IV V Ind 3Sg &SUGGEST) EXCEPT ("ator" Gram/IV INNAR Der/vv QQU Der/vv Gram/TV Gram/Refl NIAQ Der/vn N Abs Sg &typo) TARGET ("<atuinnaqqunaaq>") + &typo ; 
#Oqaasiliortut akuerisaat nunarsuarmiut eriagisassaat atuinnaqqunaaq.

#avissaar+TUQ->avissaar+TUR
ADD &typo ("<avissaartuutitsivoq>") ;
COPY ("avissaar" TUR Der/vv UTE Der/vv Gram/Reci Gram/IV &SUGGEST) EXCEPT ("avissaar" Gram/IV Gram/IV TUQ Der/vn U Der/nv Gram/IV &typo) BEFORE TIP TARGET ("<avissaartuutitsivoq>") + &typo ;
#avissaartuutitsivoq -> avissaartuutsitsivoq

#USIQvn -> UTEvn
ADD &typo ("<[Aa]kornuutsit>"r) ;
COPY ("akornut" N Abs Pl &SUGGEST) EXCEPT ("akornu" Gram/IV USIQ Der/vn N Abs Pl &typo) TARGET ("<[Aa]kornuutsit>"r) ;
#Inuuneq taanna kalaallit piat misissorniarli ila, maani akornuutsit pinnagit nikallornaqimmata.
#akornuutsit->akornutit

ADD &typo ("<[Aa]liasuutsit>"r) ;
COPY ("aliasup" Gram/IV UTE Der/vn N Abs Pl &SUGGEST) EXCEPT ("aliasup" Gram/IV Gram/IV USIQ Der/vn N Abs Pl &typo) TARGET ("<[Aa]liasuutsit>"r) ;

ADD &typo ("<apussiumaar.*>"r) ;
COPY ("apuup" Gram/TV HTR Der/vv Gram/IV GUMAAR Der/vv Gram/IV V &SUGGEST) EXCEPT ("aput" SI Der/nv Orth/Alt Gram/IV GUMAAR Der/vv Gram/IV V &typo) BEFORE MODUS TARGET ("<apussiumaar.*>"r) + &typo ;
#apussiumaarlunga

ADD &typo ("<ataatsimiitalia.*>"r) ;
COPY ("ataatsimiip" Gram/IV TIP Der/vv Gram/TV TAQ Der/vn LIAQ Der/nn N &SUGGEST) EXCEPT ("ataatsimiip" Gram/IV TUQ Der/vn LIAQ Der/nn N &typo) BEFORE KASUS TARGET ("<ataatsimiitalia.*>"r &typo) ;

ADD &typo ("<attaannut>") ;
COPY ("atsa" N Trm Sg 1SgPoss &SUGGEST) EXCEPT ("attaq" N Trm Pl 3PlPoss) TARGET ("<attaannut>") + &typo (-1 1Sg) ;

#e
#ernivissaa->erniffissaa
ADD &typo ("<ernivissaa>") ;
COPY ("erni" Gram/IV VIK Der/vn SSAQ Der/nn N &SUGGEST) EXCEPT ("erneq" VIK Der/nn SSAQ Der/nn N &typo) BEFORE KASUS TARGET ("<ernivissaa>") + &typo ;
#ernivissaa->erniffissaa

#i
#TIP+TIP+SINNAA -> TIP+HTR+SINNAA
ADD &typo ("<.*titsissinnaanissa.*>"r) ;
COPY (Gram/IV TIP Der/vv Gram/TV HTR Der/vv Gram/IV SINNAA Der/vv NIQ Der/vn SSAQ Der/nn N &SUGGEST)  EXCEPT (Gram/IV TIP Der/vv Gram/TV TIP Der/vv Gram/Db SINNAA Der/vv NIQ Der/vn Gram/TransverbNIQ SSAQ Der/nn N &typo) BEFORE KASUS TARGET &typo + ("<.*titsissinnaa.*>"r) ;
#ilinniartitsissinnaanissaq

#k
#U+VIP -> VIK+U
ADD &typo ("<kangerluuvinngikkaluarluni>") ;
COPY (VIK Der/nn U Der/nv NNGIT Der/vv GALUAR Der/vv Gram/IV V Cont 4Sg &SUGGEST) EXCEPT (U Der/nv Gram/IV VIP Der/vv NNGIT Der/vv GALUAR Der/vv Gram/IV V Cont 4Sg) TARGET ("kangerluk") + &typo ;

#m
#TAQ+TUUQ+U -> TAQ+TU
ADD &typo ("<meerartartuujugaluarluta>") ;
COPY ("meeraq" TAQ Der/nn TU Der/nv GALUAR Der/vv V Cont 1Pl &SUGGEST) EXCEPT ("meeraq" TAQ Der/nn U Der/nv Gram/IV TUUQ Der/vn U Der/nv Gram/IV GALUAR Der/vv Gram/IV V Cont 1Pl) TARGET ("<meerartartuujugaluarluta>") + &typo ;

#m
ADD &typo ("<missingersersuutit>") ;

#n
#naalaar->naalap
ADD &typo ("naalaar") (0 ("<Naalaakkersuisut>")) ;
COPY ("Naalakkersuisut" Sem/inst Sem/Hum Prop &SUGGEST) EXCEPT ("naalaar" Gram/TV TAQ Der/vn LIRSUR Der/nv Gram/TV HTR Der/vv Gram/IV TUQ Der/vn N &typo) OR ("naa" Gram/TV LAAR Der/vv Gram/TV TAQ Der/vn LIRSUR Der/nv Gram/TV HTR Der/vv Gram/IV TUQ Der/vn N &typo) BEFORE KASUS TARGET("<Naalaakkersuisut>") ;

#USIQvn->UTEvn
ADD &typo ("<[Nn]aatsorsuutsit>"r) ;
COPY ("naatsorsor" Gram/TV UTE Der/vn N Abs Pl &SUGGEST) EXCEPT ("naatsorsor" Gram/TV Gram/Refl USIQ Der/vn N Rel Pl &typo) TARGET ("<[Nn]aatsorsuutsit>"r) ;
#[Naatsorsuutsit] ataatsimut eqikkarneqarnerat ilanngunneqarpoq.
#naatsorsuutsit->naatsorsuutit

#n
#GE+VIP+TAQ -> VIK+GE+TAQ
ADD &typo ("<najugarivitaq>") ;
COPY ("najor" Gram/TV TAQ Der/vn VIK Der/nn GE Der/nv Gram/TV TAQ Der/vn N Abs Sg &SUGGEST) EXCEPT ("najor" Gram/TV TAQ Der/vn GIP Der/nv Gram/IV TIP Der/vv Gram/TV TAQ Der/vn N Abs Sg &typo) TARGET ("<najugarivitaq>" &typo) ;

WITH ("nalinginnaq" N Ins Sg) 
(1 ("atuup"))
{
ADD &typo (*) ;
COPY (U Der/nv Gram/IV TUQ Der/vn &SUGGEST) EXCEPT (* &typo) BEFORE WORDCLASS (*) ;
} ;
# Utoqqarmiut kangerluarsunnguaniit turbinip pingajua atuutilerniariarpat ungasianiit kiassaaneq [nalinginnarmik] atuutileqqissaaq.
# nalinginnarmik->nalinginnaasumik

ADD &typo ("<nusussisimanninn.*>"r) ;
COPY ("nusup" Gram/TV SIMA Der/vv HTR Der/vv Gram/IV Gram/IV NIQ Der/vn N &SUGGEST) EXCEPT ("nusup" Gram/TV HTR Der/vv Gram/IV SIMA Der/vv NIQ Der/vn NIP Der/nv Gram/IV Gram/IV NIQ Der/vn N &typo) BEFORE KASUS TARGET ("<nusussisimanninn.*>"r) + &typo ;

#o
#USIQvn->UTEvn
ADD &typo ("<[Oo]qqersuutsit>"r) ;
COPY ("oqqersuut" N Abs Sg 2SgPoss &SUGGEST) EXCEPT ("oqqi" RSUR Der/nv Gram/IV Gram/IV USIQ Der/vn N Abs Sg 2SgPoss &typo) TARGET ("<[Oo]qqersuutsit>"r) ;
#Uannut oqqersuutsit tusarpara.
#oqqersuutsit->oqqersuutit

#p
#USIQvn uu->u
ADD &typo ("<[Pp]eriuutsit>"r) ;
COPY ("periuseq" N Abs Pl &SUGGEST) EXCEPT ("periup" Gram/TV Gram/Refl USIQ Der/vn N Abs Pl &typo) TARGET ("<[Pp]eriuutsit>"r) ;
#[Periuutsit] assigiinngitsut nalimmassaanermi atorneqartut aaliangiinermilu tunngavigineqartut annikitsuinnaat pisortatigoortumik saqqummiunneqarnikuupput.
#periuutsit -> periutsit

#p
#nnaakka-gannaa
ADD &typo ("<piniannaakkatsinnik>") ;
COPY ("piniar" Gram/TV TAQ Der/vn NNAAQ Der/nn N Ins Pl 1PlPoss &SUGGEST) TARGET ("<piniannaakkatsinnik>" &typo) ;

#p
#ma-qama
ADD &typo ("<pisamanermik>") ;
COPY ("pisaqama" Gram/IV NIQ Der/vn N Ins Sg &SUGGEST) EXCEPT ("Lisa" Sem/Fem Sem/Hum Prop U Der/nv Gram/IV NIQ Der/vn N Ins Sg &typo) TARGET ("<pisamanermik>" &typo) ;

#p
#ngartu-nganartu
ADD &typo ("<pissangartulersaarutit>") ;
COPY ("pi" Gram/TV SSA Der/vv NGA Der/vv Gram/TV NAR Der/vv Gram/IV Gram/IV TUQ Der/vn LIRSAAR Der/nv UTE Der/vn N Abs Pl &SUGGEST) EXCEPT ("pissaq" NNGIR Der/nv Gram/IV Gram/IV TUQ Der/vn LIRSAAR Der/nv UTE Der/vn N Abs Pl) TARGET ("<pissangartulersaarutit>" &typo) ;

#q
#USIQvn -> U+Par
ADD &typo ("<qilammiusuutsit>") ;
COPY ("qilammiu" Orth/Arch Gram/IV V Par 2Sg &SUGGEST) EXCEPT ("qilak" MIU Der/nn TUR Der/nv Gram/IV Gram/IV USIQ Der/vn N Abs Sg 2SgPoss &typo) TARGET ("<qilammiusuutsit>") ;
# ataatarput [qilammiusuutsit] aqqit illernarsili
#qilammiusuutsit -> qilammiuusutit

#q
#TA+SUP -> TA
ADD &typo ("<quilertasullunga>") ;
COPY ("quilerta" Gram/IV V Cont 1Sg &SUGGEST) EXCEPT ("quilertasoq" LIUTE Der/nv Gram/TV Gram/TV V Imp 2Sg 1SgO Orth/Arch &typo) TARGET ("<quilertasullunga>" &typo) ;

#s
#USIQvn -> UTEvn
ADD &typo ("<[Ss]umiorpaluutsit>"r) ;
COPY ("sumiorpaluut" N Abs Pl &SUGGEST) EXCEPT ("sumioq" PALUP Der/nv Gram/IV Gram/IV USIQ Der/vn N Abs Pl &typo) TARGET ("<[Ss]umiorpaluutsit>"r) ;
#[Sumiorpaluutsit] atuarfimmi ersersinneqassapput.
#sumiorpaluutsit->sumiorpaluutit

#t
#USIQvn -> UTEvn
ADD &typo ("<taaguutsi.*>"r) ;
COPY ("taagor" Gram/TV UTE Der/vn N &SUGGEST) EXCEPT ("taagor" Gram/TV Gram/Refl USIQ Der/vn N) BEFORE KASUS TARGET ("<taaguutsi.*>"r &typo) ;
#Nuersaanermi taaguutsit

### possible base errors ###
ADD &typo ("<atimik>") ;
COPY ("ateq" N Ins Sg &SUGGEST) EXCEPT ("ate" N Ins Sg) TARGET ("<atimik>") (1 QAR) ;
#[atimik] qinnuteqarpoq

### possible inflection errors ###
ADD &typo ("<[Tt]usagassiutigut>"r) ;
COPY (Via Pl &SUGGEST) EXCEPT (Abs Pl 1PlPoss Orth/Alt &typo) TARGET (/"tusar"\ Gram/TV\ TAQ\ Der/vn\ SSAQ\ Der/nn\ LIUR\ Der/nv\ Gram/TV\ UTE\ Der/vn/l) (1 ("nalunaar")) ;
#[Tusagassiutigut] nalunaaruteqarnikkut killiffimmik malinnaatitsoqarumaarpoq.
#Tusagassiutigut->Tusagassiutitigut

### possible propernoun, but analysis is noun
# loanword in Err/Sub only w or w/o epenthesis -> suggest form w/o Err/Sub
ADD &typo ("<\\p{Lu}?\\p{Ll}+\\u002Di?\\p{Ll}+>"r) (0 ("\\p{Ll}+"r)) ;
COPY (&SUGGEST) EXCEPT (Err/Sub &typo) TARGET ("<\\p{Lu}?\\p{Ll}+\\u002Di?\\p{Ll}+>"r) + &typo ;
#Beredskabsstab-ip -> Beredskabsstabip
#generator-ianiit -> generatorianiit

#=============#
#MISSING SPACE
#=============#
###20240607 udkommenteret midlertidigt, trænger til større undersøgelse først, ellers klipper de bare alle mulige steder, som de ikke burde
###missing space ###
#liste at få kigget på senere: ingerlaneqajortut
#COPY ("<$1 $2>"v SUGGESTWF) TARGET internword ;
#kikkuttamarmik
#COPY ("<$1 $2>"v SUGGESTWF) TARGET internmood ;
#oqaluttuaritoqarpoq
#Paaserusuinnarparaoqarpoq
#Ajunngilaqeqippaanga
COPY ("<$1 $2>"v &SUGGESTWF) TARGET interncase + HeurVerb ;
#ingerlaneraniarlaleriarlugu
COPY ("<$1 $2>"v &SUGGESTWF) TARGET internword ;
#pinninneqajortoq
#===


#===================================#
#EXPERIMENTAL might be removed later#
#===================================#
# space error
LIST syncope = "<\(.+r\)>"r "<\(.+nng\)>"r ;
COPY ("<$1$2>"v SUGGESTTWF) TARGET  ("<.*>"r) + syncope (1 ("<.*>"r) + syncope) ; # super AD HOC might remove later if theres no pattern
COPY ("<$2$1>"v SUGGESTTWF) TARGET  ("<.*>"r) + syncope (-1 ("<.*>"r) + syncope) ; # super AD HOC might remove later if theres no pattern
# Oqar navianng # even put together it is still syncoped and has no analysis
#===


ADD:spelled (&typo &SUGGESTWF) (<spelled>) 
(NEGATE 0 HeurVerb) 
(NEGATE 0 internword) 
(NEGATE 0 &typo) ;

END
#ADD:Err/Orth (&typo) (Err/Orth) (NOT 0 (*) - (Err/Orth)) ;
#COPY:Err/Orth (&SUGGEST) EXCEPT (Ex/N Ex/V Ex/A Ex/IV Ex/TV Err/Orth) TARGET (Err/Orth &typo) ;
