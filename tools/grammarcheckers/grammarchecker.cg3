# -*- cg-pre-pipe: "hfst-tokenise -g /usr/share/giella/kal/tokeniser-disamb-gt-desc.pmhfst | vislcg3 -g ../../src/cg3/valency.cg3 | vislcg3 -g ../tokenisers/mwe-dis.cg3" -*-
#zsh -l -c '~/langtech/kal/tools/grammarcheckers/modes/kal-before-gram.mode'


# Divvun & Giellatekno - open source grammars for Sámi and other languages
# Copyright © 2000-2024 UiT The Arctic University of Norway
# http://giellatekno.uit.no & http://divvun.no
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version. The GNU General Public License
# is found at http://www.gnu.org/licenses/gpl.html. It is
# also available in the file $GTHOME/LICENSE.txt.
#
# Other licensing options are available upon request, please contact
# giellatekno@hum.uit.no or divvun@hum.uit.no

# ==================================================================== #
#!! #      G R E E N L A N D I C   G R A M M A R   C H E C K E R
# ==================================================================== #
# ====================================================================

#  Sámi language technology 2000-2024, UiT The arctic University of Norway #

# ---------------------- #
#   Development setup:   #
# ---------------------- #
#
#!! ```
#!!  In the catalogue for kal, do:
#!!  ./autogen.sh
#!!  ./configure --enable-grammarchecker --enable-spellers
#!!  make -j
#!!  cd tools/grammarcheckers
#!!  make dev
#
#!!   Then test as follows:
#
#!! echo "e Nerisassiornermut soqutigisaqarpit?|sh modes/trace-kalgram.mode  # from the terminal
#!!
#!! Or eventually, write
#!! `make check`
#!!




#!! ## Tag declaration

#!! ### Import tag declarations
#!! We import tag declaration from `../../src/cg3/disambiguator.cg3`

INCLUDE STATIC liststemplates.cg3 ;



#!! ## Rule section

BEFORE-SECTIONS
#heuristic tags in order to correct other parts of sentence
ADD <heur-prop> ("\\p{Lu}.*"r ?) (NEGATE -1 BOS) ;
ADD <heur-Abs-Sg> ("\\p{Lu}.*a"r ?) (-1 (/UTE\ Der/vn\ N\ Abs\ Sg/l)) ;
#[Aqqama]: Inuit aqqinik qinnuteqaat Aqqama ilissinnut akuerisassatut ingerlateqqippara. Tunngavilersuutingineqarneri taajuutaavoq aammalu allattaasitaamut naapertuulluni.

ADD <sem-personalname> ("ateq") (0 (/3[SP][gl]Poss/r)) (-1 ("inuk" Rel)) ;


SECTION

#!! ### Speller suggestions rule
#!! add &SUGGESTWF to any spelling suggestion
#!! that we actually want to suggest to the user.
#!! The simplest is to just add it to all spelled words:

#€# = vi slår af får vi tester
#€# ADD:spell-it-all (&typo &SUGGESTWF) (<spelled>) ;
#€# #!! @RULENAME@
#€#
#€# #!! But perhaps you want to only suggest spellings of words that are not inside "quotes":
#€#
#€# ADD:spell-conservatively (&typo &SUGGESTWF) (<spelled>) IF (NEGATE -1 QMARK LINK 2 QMARK);
#!! @RULENAME@

#!! ### Grammatical rules

#!! #### Verb valency rules


# 
#ADD:msyn-arg-trm-ins (&msyn-arg-trm-ins) TARGET Trm IF
#	(NOT 0 ("<ataatsimut>"))
#	(1 QAR LINK NOT 0 (/"pi"\ NGAAR\ Der/vv\ Gram/IV\ Gram/IV\ UTE\ Der/vn\ QAR/l) OR (/"sunner"\ Gram/TV\ UTE\ Der/vn/l) OR #(/"oqaaseq"\ SSAQ/l) OR Trm)

#	;

#COPY:msyn-arg-trm-ins (Ins &SUGGEST) EXCEPT (Trm  &msyn-arg-trm-ins) BEFORE NUMERUS TARGET (&msyn-arg-trm-ins);
#!! @RULENAME@

#err Nerisassiornermut soqutigisaqarpit? # er du interesseret til madlavning
#ok  Nerisassiornermik soqutigisaqarpit? # er du interesseret i madlavning


ADD:msyn-arg-ins-trm (&msyn-arg-ins-trm) TARGET Ins IF (1 Gram/Reci + V);
#!! @RULENAME@

COPY:msyn-arg-ins-trm (Trm &SUGGEST) EXCEPT (Ins  &msyn-arg-ins-trm) BEFORE NUMERUS TARGET (&msyn-arg-ins-trm);
#!! @RULENAME@

	# absolutive > relative case
	
ADD:msyn-arg-abs-rel (&msyn-arg-abs-rel) TARGET Abs IF
	(0 Sem/Hum LINK NEGATE 0 FIRSTNAME)
	(*1 (N Abs) - Sem/Hum BARRIER (/Gram/IV\ V/l) + Par LINK *1 (/Gram/TV\ V/l) - GE - FossileredeCont BARRIER (/Gram/IV\ V/l) + Par)
	(NEGATE 1 1SgPoss LINK 1 ContNeg LINK *1 VFIN LINK 0 1Sg)
	;
#!! @RULENAME@
	## Judithe nanoq takuaa.
	#$ {Judithep} nanoq takuaa.

COPY:msyn-arg-abs-rel (Rel &SUGGEST) EXCEPT (Abs &msyn-arg-abs-rel) BEFORE NUMERUS TARGET (&msyn-arg-abs-rel);
#!! @RULENAME@

ADDRELATION:msyn-arg-abs-rel ($2) (V) FROM (*-1 Prop LINK 0 (&msyn-arg-abs-rel)) ;

	# relative > absolutive case
	
ADD:msyn-subj-rel-abs (&msyn-subj-rel-abs) TARGET Rel IF
	(*1 (/Gram/IV\ V\ /l) BARRIER POSSESSUM)
	(0 (/[1-3][SP][gl]Poss/r))
	;
#!! @RULENAME@
	## Qilanaarilluinnakkama PIVIUSUNNGULERPOQ.
	#$ {Qilanaarilluinnagara} PIVIUSUNNGULERPOQ.
	## {Qilanaarilluinnakkavit} PIVIUSUNNGULERPOQ

#COPY:msyn-subj-rel-abs (Abs VSTR:$1 &SUGGEST) EXCEPT (Rel /[SP][gl]/r &msyn-subj-rel-abs) BEFORE (/[1-3][SP][gl]Poss/r) TARGET (&msyn-subj-rel-abs) (*1 (/Gram/IV\ V\ /l) LINK 0 (/\([SP][gl]\)/r));

COPY:msyn-subj-rel-abs (Abs VSTR:$1 &SUGGEST) EXCEPT (Rel VSTR:$1 &msyn-subj-rel-abs) BEFORE (/[1-3][SP][gl]Poss/r) TARGET (&msyn-subj-rel-abs) (0 (/[SP][gl]/r)) (*1 (/Gram/IV\ V\ /l) LINK 0 (/^3\([SP][gl]\)$/r));

#COPY:msyn-subj-rel-abs (Abs Pl &SUGGEST) EXCEPT (Rel Pl &msyn-subj-rel-abs) BEFORE (/[1-3][SP][gl]Poss/r) TARGET (&msyn-subj-rel-abs) (*1 (/Gram/IV\ V\ /l) LINK 0 3Pl);
#!! @RULENAME@

#ADDRELATION:msyn-subj-rel-abs ($2) (V) FROM (*-1 Prop LINK 0 (&msyn-subj-rel-abs)) ;


#=========================================
# Judithe påbegyndt forsøgsvist 20240510 #
#=========================================
#codes used in structural descriptions:
#n = noun
#v = verb stem
#[ ] = word in question
#+CASE* cases in agreement


### Nouns ###

# Abl
#AD HOC phrase specific but extremely common
#uummammik pisumik
ADD:msyn-ins-abl &msyn-ins-abl TARGET Ins (0 ("uummat") LINK 1 (/"pi"\ Gram/IV\ TUQ\ Der/vn\ N\ Ins\ Sg/l)) ;
COPY:msyn-ins-abl (Abl &SUGGEST) EXCEPT (Ins &msyn-ins-abl) BEFORE NUMERUS TARGET &msyn-ins-abl ;
#[uummammik] pisumik

#-miik -> -miit
ADD:msyn-excl-abl &msyn-excl-abl TARGET (Lok Gram/Exclm) (0 ("<.*miik>"r) LINK 0* V) ;
COPY:msyn-excl-abl (N Abl Sg &SUGGEST) EXCEPT (N Lok Sg Gram/Hyb IP Gram/IV V Ind Gram/Exclm) TARGET &msyn-excl-abl ;
#Atuartullu anartarfiat atuarfiup tunuaniippoq immikkoorluni [atuarfimmiik].
#[Illoqarfimmiik] allaminngaanneerlunga ilinniariartuuvunga.
#Takugamisigummigooq [anartarfimmiik] anillaqatigiittugut. 
#Aappassaa taamak oqarmat atsamma [sinittarfimmiik] suaamisaarlugu akisimavaa
#Tassa [ungasissumiik] seqernup qinngornera atorlugu qaammartitsisaarut.
#Assulli kamaammerpunga [ersinngitsukasimmiik] nipangeqquneqarama.

# Rel
ADD:msyn-abs-rel &msyn-abs-rel TARGET ("akunneq") + Abs + Sg (1 ("tulleq") + 3SgPoss) ;
#Sap.-[akunnera] tulliani ataasinngornermit sisamanngornermut (27.-30. maaji) studiedagissama ilaat atussagakkit sulissanngilanga. 
ADD:msyn-abs-rel &msyn-abs-rel TARGET Sem/Geo + Abs (1 ("sila") + 3SgPoss) ; 
#[Nuuk] silaa qanga ajorami
ADD:msyn-abs-rel &msyn-abs-rel TARGET Abs (1 NIQ + SSAQ + 3SgPoss) ;
COPY:msyn-abs-rel (Rel &SUGGEST) EXCEPT (Abs &msyn-abs-rel) BEFORE NUMERUS &msyn-abs-rel ;

# Trm
# NIQ+Trm atatillugu
ADD:msyn-abs-trm-atatillugu &msyn-abs-trm-atatillugu TARGET NIQ + Abs + 4SgPoss (1 ("ata" Gram/IV TIP Der/vv Gram/TV Gram/TV V Cont 3SgO )) ; 
COPY (Trm &SUGGEST) EXCEPT (Abs &msyn-abs-trm-atatillugu) BEFORE NUMERUS TARGET &msyn-abs-trm-atatillugu ;
#[Ilinniarnini] atatillugu Qiviumi nioqqutissanik nutaanik sananikuuvoq

# Via

#[n]+CASE* n+CASE*
ADD:xmsyn-abs-via &msyn-abs-via TARGET Abs (0 Sg LINK 1 Via) (1* VFIN + 1Sg BARRIER V) ;
ADD:xmsyn-abs-via &msyn-abs-via TARGET Abs (0 Sg + NON_HUMAN LINK 1 Via) (1* VFIN + 3Sg + humverb BARRIER V) ;
COPY:msyn-abs-via (Via &SUGGEST) EXCEPT (Abs &msyn-abs-via) BEFORE NUMERUS TARGET &msyn-abs-via ;
ADDRELATION:msyn-abs-via ($2) (Via) TO (-1 Via LINK 0 &msyn-abs-via) ;
#Bussit apuummata [matu] tunorlikkut iserpunga, bussillu tunuaniiginnalerlunga.


# Case agreement

#[n]+CASE* taama v+TIGE+TUQ+CASE*
ADD:msyn-abs-taama-case &msyn-abs-taama-case TARGET Abs + $$NUMERUS + measurable-n (T:measuring-v) ;
#Kinalu isersimaniarpa [tupinnartoq] taama angitigisumi
#[illu] taama angitigisumi asaasoqarpoq
ADD:msyn-abs-taama-case &msyn-abs-taama-case TARGET Abs + $$NUMERUS + opinionable-n (T:opinion-v) ;
#Ilalumi aamma kanngugipajuppara [sila] taama alianaatsigisumi tammartajaarsimalluni uternissaq.
ADD:msyn-abs-taama-case &msyn-abs-taama-case TARGET Abs + $$NUMERUS + countable-n (T:quantitative-v) ;
#[ukiut] taama amerlatigisuni inuusuttunik atuartitsisarsimavoq
COPY:msyn-abs-taama-case (VSTR:$1 &SUGGEST) EXCEPT (Abs &msyn-abs-taama-case) BEFORE NUMERUS TARGET &msyn-abs-taama-case (2 (/^\(Rel|Abl|Via|Aeq|Ins|Trm|Lok\)$/r)) ; 

# Number agreement
WITH ("inuk") + Rel + Pl 
(1 ("ateq") + Ins + Pl + 3PlPoss + <sem-personalname>)
(*1 ("qinnut" QAR UTE) + $$KASUS + $$NUMERUS LINK 1 <heur-prop>) 
(*1 <heur-prop> + <heur-Abs-Sg> + $$KASUS + $$NUMERUS OR Prop + $$KASUS + $$NUMERUS LINK NOT 1 Conj)
{
ADD:xmsyn-personalname-Pl-Sg 
co&msyn-personalname-Pl-Sg (*) ;
COPY:xmsyn-personalname-Pl-Sg 
(Rel Sg &SUGGEST) EXCEPT (Rel Pl &msyn-Pl-Sg) OR (Rel Pl 2SgPoss &msyn-Pl-Sg) AFTER WORDCLASS TARGET (*) ;
ADD:xmsyn-personalname-Pl-Sg 
&msyn-personalname-Pl-Sg (_C2_) ;
COPY:xmsyn-personalname-Pl-Sg 
(Sg 3SgPoss &SUGGEST) EXCEPT (Pl 3SgPoss &msyn-Pl-Sg) AFTER KASUS TARGET (_C2_) ;
ADDRELATION ($2) (_C1_) (0 <sem-personalname>) TO (jC2 (*)) (0 <heur-prop> OR Prop) ;
} ;
#[Inuit aqqinik -> Inuup aqqanik]: Inuit aqqinik qinnuteqaat Aqqama ilissinnut akuerisassatut ingerlateqqippara.

### verbs ###
#Con
ADD:msyn-2Pl1Pl0-1Pl3SgO &msyn-con-2Pl1Pl0-1Pl3SgO  (2Pl 1PlO) + Con (-1 (Abs Sg)) ;
COPY (1Pl 3SgO &SUGGEST) EXCEPT (2Pl 1PlO &msyn-con-2Pl1Pl0-1Pl3SgO) TARGET &msyn-con-2Pl1Pl0-1Pl3SgO ; 
#Taaguullu skærm [pigereersimagutsigut], assut utersaarfigitikkusukkaluarpunga taajutaanik.

### agreement ###
ADD:msyn-obj-marking-abs-3PlO &msyn-obj-marking-abs-3PlO TARGET (HTR Der/vv Gram/IV) IF (*-1 Abs BARRIER (*) - Abs LINK *-1 Rel BARRIER (*) - Rel);

#CASE, PERSON, NUMBER
WITH (1Sg 2SgO) + Opt
(-1 Pron + Abs + Pl)
{
ADD:msyn-Opt-1Sg-2SgO &msyn-Opt-1Sg-2SgO (*) ;
COPY (LAAR Der/vv Gram/TV V Imp 2Sg 3PlO &SUGGEST) EXCEPT (V Opt 1Sg 2SgO &msyn-Opt-1Sg-2SgO) (*) ;
ADDRELATION ($2 LEFT) (_C1_) TO (1 &msyn-Opt-1Sg-2SgO) ;
} ;
# [takulakkit -> takulaakkit] Immaqa to pillugu ilitsersuummik allassimasoqanngilaq, kisianni uku takulakkit.

#FEJL
#ilinniartitsisup meeqqat ilinniartitsivoq REL ABS HTR
#KORREKTIONER
#ilinniartitsisup meeqqat {ilinniartippai} (objektsmarkering på verbet + fjerne heltransitivitet) REL ABS (*HTR 3PlO)
#{ilinniartisisoq meeqqanik} ilinniartitsivoq (instrumentalis på objektet og subjektet til absolutiv) --- ABS INS HTR

#PERS/NUM AGREEMENT ABS-3PlO - msyn-obj-marking-abs-3PlO
#HTR-SUBJ-ABS - msyn-subj-marking-abs-HTR


#ilinniartitsisoq angajukkunnik ilinniartitsivoq
#rettelse 1: ilinniartitsisup angajukkut ilinniartippai
#rettelse 2: ilinniartitsisoq angajukkunnik ilinniartitsivoq

#COPY:msyn-abs-agreement (3PlO &SUGGEST) EXCEPT (HTR Der/vv Gram/IV) TARGET &msyn-abs-agreement IF (*-1 (Pl Abs));

COPY:msyn-obj-marking-abs-3PlO (VSTR:3$1O &SUGGEST) EXCEPT (HTR Der/vv Gram/IV) TARGET &msyn-obj-marking-abs-3PlO IF (*-1 (/\([PS][lg]\)/r Abs));

#ilinniar+TIP+Gram/TV+V+Ind+3Sg
#"ilinniar" Gram/IV TIP Der/vv Gram/TV Gram/TV V Ind 3Sg 3PlO


ADD:msyn-obj-marking-abs-3PlO &msyn-obj-marking-abs-3PlO TARGET Abs IF (*1 (HTR Der/vv Gram/IV &msyn-obj-marking-abs-3PlO)) ;


ADDRELATION ($2 LEFT) (HTR Der/vv Gram/IV &msyn-obj-marking-abs-3PlO) TO (*-1 (Abs &msyn-abs-agreement));

#COPY:msyn-obj-marking-abs-3PlO (SUGGEST) TARGET (Abs &msyn-obj-marking-abs-3PlO) ;

ADD:msyn-obj-marking-abs-3PlO &msyn-obj-marking-abs-3PlO TARGET Rel IF (*1 (Abs &msyn-obj-marking-abs-3PlO)) ;

ADDRELATION ($3 LEFT) (HTR Der/vv Gram/IV &msyn-obj-marking-abs-3PlO) TO (*-1 (Rel &msyn-obj-marking-abs-3PlO));

#COPY:msyn-obj-marking-abs-3PlO (SUGGEST) TARGET (Rel &msyn-obj-marking-abs-3PlO) ;

SUBSTITUTE (&msyn-obj-marking-abs-3PlO) (co&msyn-obj-marking-abs-3PlO) TARGET (SUGGEST);
MAP:LOCK_READING (SUGGEST) (SUGGEST);

# forslag 2

ADD:msyn-subj-marking-abs-HTR &msyn-subj-marking-abs-HTR TARGET (HTR Der/vv Gram/IV)  IF (*-1 Abs BARRIER (*) - Abs LINK *-1 Rel BARRIER (*) - Rel) ;

COPY:msyn-subj-marking-abs-HTR (SUGGEST) TARGET (HTR Der/vv Gram/IV &msyn-subj-marking-abs-HTR);

ADD:msyn-subj-marking-abs-HTR &msyn-subj-marking-abs-HTR TARGET Abs IF (*1 (HTR Der/vv Gram/IV &msyn-subj-marking-abs-HTR)) ;

ADDRELATION ($2 LEFT) (HTR Der/vv Gram/IV &msyn-subj-marking-abs-HTR) TO (*-1 (Abs &msyn-subj-marking-abs-HTR));

COPY:&msyn-subj-marking-abs-HTR (Ins SUGGEST) EXCEPT (Abs) BEFORE NUMERUS TARGET (Abs &msyn-subj-marking-abs-HTR);

ADD:msyn-subj-marking-abs-HTR &msyn-subj-marking-abs-HTR TARGET Rel IF (*1 (Abs &msyn-subj-marking-abs-HTR) BARRIER (*) - Abs);

ADDRELATION ($3 LEFT) (HTR Der/vv Gram/IV &msyn-subj-marking-abs-HTR) TO (*-1 (Rel &msyn-subj-marking-abs-HTR));

COPY:msyn-subj-marking-abs-HTR (Abs SUGGEST) EXCEPT (Rel) BEFORE NUMERUS TARGET (Rel &msyn-subj-marking-abs-HTR);


SUBSTITUTE (&msyn-subj-marking-abs-HTR) (co&msyn-subj-marking-abs-HTR) TARGET (SUGGEST);
MAP:LOCK_READING (SUGGEST) (SUGGEST);

#Verb to noun
WITH Con + 3Sg + UGEDAGE
(1 ("tunge") + 3SgPoss)
{
ADD:msyn-con-rel &msyn-con-rel (*) ;
COPY:msyn-con-rel ("ataaseq" NNGUR Der/nv Gram/IV Gram/IV Gram/IV NIQ Der/vn N Rel Sg &SUGGEST) EXCEPT ("ataaseq" NNGUR Der/nv Gram/IV Gram/IV Gram/IV V Con 3Sg &msyn-con-rel) (*) ;
#ADD
ADDRELATION:msyn-con-rel ($2) (*) TO (jC1 (*) LINK 0 ("tunge")) ; #virker ikke uden LINK 0 ("tunge") :(
} ;
#ataasinngorpat->ataasinngornerup: Kisianni aalisartut aallartinnginnerminni ataasinngorpat tungaanut naammagittaqqaalaartariaqassapput.

### word order ###

WITH (N Abs) + $$NUMERUS - ADJEKTIVISK IF
    (*-1 (3SgPoss Abs) + $$NUMERUS + ADJEKTIVISK BARRIER (*) - Abs LINK *-1 BOS LINK 1 (N Rel Sg)) # _C1_
    (NEGATE *0 &msyn-obj-marking-abs-3PlO) # _C2_
    (NEGATE 0  &msyn-obj-marking-abs-3PlO) # _C3_
{
  ADD:syn-abs-wordorder (&syn-abs-wordorder DELETE) (*);
  COPYCOHORT:syn-abs-wordorder (&ADDED co&syn-abs-wordorder) # These tags will be added to the copy
     EXCEPT (DELETE &syn-abs-wordorder)    # We don't include this tag in the copy
     TARGET    (*)       # Copy from the main WITH target
     TO BEFORE (jC1 Rel) # The copy ends up before the first WITH context
     ;
  ADDRELATION:syn-abs-wordorder ($3 LEFT) (*) TO (jC1 (*) LINK -1 (&ADDED));
};

SUBSTITUTE ("<\\p{Lu}.*>"r) ("<$2$1>"v) TARGET ("<\\p{Lu}\(.*\)>"r) (0 ("\(\\p{Ll}\).*"r)) (-1 (co&syn-abs-wordorder &ADDED)) ; #downcase ex -1 BOS if baseform is not a propernoun

# {Uunnaaviup sanianiittoq igalaaq} qiverseriarlugu uunnaavik qamissallugu, eqqarsaammerujussuarpunga. >  Igalaaq uunnaaviup sanianiittoq qiverseriarlugu uunnaavik qamissallugu, eqqarsaammerujussuarpunga. # the.kettle which.is.next.to the.window > the.window the.kettle is.next.to
# Nakutitseriarnerisa nallinnaannartut {tunuanniittoq iluarinninnera} takusinnaasimasariaqaraluarpara. > Nakutitseriarnerisa nallinnaannartut  iluarinninnera tunuanniittoq takusinnaasimasariaqaraluarpara.

## adjektivisk nomen substantivisk nomen > substantivisk nomen adjektivisk nomen ##
WITH (/TUQ\ Der/vn\ N\ /l) + $$NUMERUS + $$KASUS IF
     (1 N + $$NUMERUS + $$KASUS - TUQ - ADJEKTIVISK)
     (NEGATE *0 &msyn-obj-marking-abs-3PlO)
     (NEGATE 0 &msyn-obj-marking-abs-3PlO)
{
  ADD:syn-TUQ-wordorder (&syn-TUQ-wordorder DELETE) (*);
  
  COPYCOHORT:syn-TUQ-wordorder (&ADDED co&syn-TUQ-wordorder)
  	EXCEPT (&syn-TUQ-wordorder DELETE)
  	TARGET (*)
  	TO AFTER (jC1 N)
  	;

  ADDRELATION:syn-TUQ-wordorder ($2 RIGHT) (*) TO (jC1 (*) LINK 1 (&ADDED));
};
  
# Gangersuarmi pisuttuarujoortalerpunga, inuk kisima nipangissananga {nipikitsumik niaqunniittut nipit} eqqumiitsut oqaluuterujoortalerlugit.



END
#nedenstående virker selvsagt ikke. tegnsætningstegn genereres ikke for sig, og akronymer med heuristisk analyse kan heller ikke genereres.
#SPLITCOHORT ("<$1>"v "$1"v <acro-apo> "<$2>"v "$2"v <acro-case>) ("<(\\p{Lu}+\\u0027)\(mik\)>"r ?) ;
#SPLITCOHORT ("<$1>"v "$1"v <acro> "<$2>"v "$2"v <apo>) ("<(\\p{Lu}+)(\\u0027)>"r) (0 (<acro-apo>)) ;
#ADD:apsthr-hyph (&apo-hyph) (<apo>) ;
#COPY ("-"r <hyph> &SUGGEST) EXCEPT ("'"r <apo> &apo-hyph) TARGET (&apo-hyph) ;

# The method is: Add &SUGGEST to a copied reading to *generate* a
# suggestion form from that reading. The copy should contain the error
# tag too – &-prefixed error tags are ignored when generating, but
# used to create human-readable messages.

#!! ### Simple punctuation rules

#!! Rules for quotation marks.

#€# = vi slår af får vi tester

#€# ADD:use-guillemets (&guillemets) TARGET (""");
#€# #!! @RULENAME@
#€# COPY:left-guillemet ("«" &SUGGEST) EXCEPT (""") TARGET (&guillemets) ;
#€# #!! @RULENAME@
#€# COPY:right-guillemet ("»" &SUGGEST) EXCEPT (""") TARGET (&guillemets) ;
#€# #!! @RULENAME@
#€#
#€# ADD:use-ellipsis (&ellipsis) TARGET ("...");
#€# #!! @RULENAME@
#€# COPY:use-ellipsis ("…" &SUGGEST) EXCEPT ("...") TARGET (&ellipsis) ;
#€# #!! @RULENAME@


#===========================================

#JD: examples to look at later
#Oqaluttualiornermi unammisitsinermi Niviaq Korneliussen ajugaasut ilagaat ilaatigut uanga, kalaallillu taalliortartup eqqumiitsuliortartullu Jessie Kleemannip [suleqatiginissaa] eqqugassaalluni.
