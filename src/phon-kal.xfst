! =============================== !
! Hyphenator and phon for West Greenlandic !
! =============================== !

echo <<defining Cns and Vow...>>

define Vow       [ a | á | e | i | o | u | y | æ | ø | å | ä | ö | 
                   A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö | 
é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï | â | ê | ô | û | î | ã | ý |
É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï | Â | Ê | Ô | Û | Î | Ã | Ý ] ;

define KalVow [ a | e | i | o | u | A | E | I | O | U ] ;

define ForVow [ Vow - KalVow ] ;

define LongVow [ a a | A a | A A | e e | E e | E E | i i | I i | I I |
                 o o | O o | O O | u u | U u | U U | a i | A i | A I ] ;


define A2 [ a | A | i | I ] ;
define E2 [ e | E ] ;
define I2 [ i | I ] ;
define O2 [ o | O ] ;
define U2 [ u | U ] ;

define Cns [ b | c | č | d | đ | ð | f | g | h | j | k | l | m | n | ŋ | 
             B | C | Č | D | Đ | Ð | F | G | H | J | K | L | M | N | Ŋ | 
             ñ | p | q | r | s | š | t | ŧ | v | w | x | z | ž |   
             Ñ | P | Q | R | S | Š | T | Ŧ | V | W | X | Z | Ž ] ; 

define S [ S | s ] ; 

define Cnss [ Cns - S ] ;

define ptk [p|t|k];

!define Sgm [ Cns | Vow ] ;

define Dummy [ %^K ] ;

echo << Rules>>
define NG          [..] -> %.  || Vow ([r|R|n|N]) _ [ n g | N G ] ;
define beforeCV    [..] -> %.  || Vow           _ Cns Vow ;  !a-te-qu-seq
define sC          [..] -> %.  || Vow S Cns*    _ Cns Vow ;
define Css         [..] -> %.  || Vow Cns S     _ S ;
define CCV         [..] -> %.  || Vow Cnss+     _ Cns Vow ;  !il-lu, Matt-hæ-u-sip
define CsCV        [..] -> %.  || Vow Cnss+     _ S Cnss Vow ; !Kron-stadt, Kris-tus
define keep       [..] -> %^K || [a|A]         _ A2 ,
                                  [e|E]         _ E2 ,
                                  [i|I]         _ I2 ,
                                  [o|O]         _ O2 ,
                                  [u|U]         _ U2 ; 
define VV          [..] -> %.  || Vow           _ Vow ;      !Matt-hæ-u-sip

echo << Cleanup...>>
define clean  Dummy -> 0 ;

! Lagre denne fila: i xfst: source < dennefila.txt og deretter: save hy.fst
! cat fil.txt | preprocess | ./lookup -utf8 hy.fst | cut -f2 | tr '\n' ' ' | less

echo <<Phonology...>>

define down A -> a, B -> b, C -> c, D -> d, E -> e, F -> f, G -> g, H -> h, 
    I -> i, J -> j, K -> k, l -> L, M -> m, N -> n, O -> o, P -> p, Q -> q, 
    R -> r, S -> s, T -> t, U -> u, V -> v, W -> w, X -> x, Y -> y, Z -> z, 
    Æ -> æ, Ø -> ø, Å -> å ;


define aaptk  a a -> æ %: || _ ptk ;
define aiaptk     a -> æ || _ [ ptk | i .#. ] ;
define aaiiuu a a -> ɑ %: , i i -> e %: ,  u u -> u %: || _ [q|r] ;
define aiu       a -> ɑ    ,   i -> e ,        u -> o   || _ [p|q] ;
define ng     n g -> ŋ ;
define nng    n -> ŋ || _ %. ŋ ;
define gg   g %. g -> ç %. ç ;
define g      g -> ɣ ;
define dbl l %. l -> l̥ %. l̥ , q %. q ->  ʔ %. ʔ , r %. r -> R %. R ;
define v v -> ʋ || Vow %. _ Vow ;

define t1 [..] -> ¹ || %. _ (Cns) Vow [Vow|Cns|%:] ;
define t2 [..] -> ² || %. _ (Cns) Vow [Vow|Cns]  Cns ;

echo <<Combining...>>

read regex [ down
# Syll
.o. NG
.o. beforeCV 
.o. sC  
.o. Css 
.o. CCV 
.o. CsCV
.o. keep
.o. VV 
# Phon
.o. aaptk
.o. aiaptk
.o. aaiiuu
.o. aiu
.o. ng
.o. nng
.o. gg
.o. g
.o. dbl
.o. v
# Stress
.o. t1
.o. t2
# Clean
.o. clean ] ;




invert net ;
