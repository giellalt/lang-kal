
! =================== !
! Arabiske numeralier !
! =================== !

! Vi deler arabiske tal i to. Først dei før komma, som er
! uavhengig av danske ordformer.
! Deretter dei etter komma, som er avhengig av dansk.

LEXICON numeralier
 arabiskeførkomma ;   ! Ein vokaluavhengig automat
 arabiskeefterkomma ; ! Ein vokalavhengig automat

LEXICON arabiskeefterkomma
 arabiske ;
 naturligetal ;
    1t9 ;   ! 1-9
    10t99 ; ! 10-99

! Før komma

LEXICON arabiskeførkomma

 %0 talkomma ; 
  1 talkomma ;
  2 talkomma ;
  3 talkomma ;
  4 talkomma ;
  5 talkomma ;
  6 talkomma ;
  7 talkomma ;
  8 talkomma ;
  9 talkomma ;
	# ;
! Skal være en konsonantnøytral automat som
! deretter går til talkomma og derfra til numeralier.

LEXICON talkomma
 arabiskeførkomma ;
 komma ;

LEXICON komma
 , efterkomma ;
 . efterkomma ;
 / efterkomma ;
 - efterkomma ;


LEXICON efterkomma
 arabiskeførkomma ;
 arabiskeefterkomma ;
 
! Efter komma  

LEXICON arabiske
 %0 loop ; 
  1 loop ;
  2 loop ;
  3 loop ;
  4 loop ;
  5 loop ;
  6 loop ;
  7 loop ;
  8 loop ;
  9 loop ;

LEXICON loop
 arabiske ;
 1t9sep ;
 
 
LEXICON naturligetal
  1 1t9sep ;
  2 1t9sep ;
  3 1t9sep ;
  4 1t9sep ;
  5 1t9sep ;
  6 1t9sep ;
  7 1t9sep ;
  8 1t9sep ;
  9 1t9sep ;  
    
LEXICON 1t9
 1567 ;
 23489 ;
 
LEXICON 1567 
 1 num_C ;
 5 num_C ;
 6 num_C ;
 7 num_C ;
 
LEXICON 23489      
 2 num_V ;
 3 num_V ;
 4 num_V ;
 8 num_V ;
 9 num_V ;
 
LEXICON 10t99    
  1 tenC ;
  1 tenV ; 
  2 20t40 ;
  3 20t40 ;
  4 20t40 ;
  5 50t90 ;
  6 50t90 ;
  7 50t90 ;
  8 50t90 ;
  9 50t90 ;

LEXICON tenV ! 10, 11
 %0 num_V ; 
  1 num_V ;

LEXICON tenC ! 12-19
  2 num_C ;
  3 num_C ;
  4 num_C ;
  5 num_C ;
  6 num_C ;
  7 num_C ;
  8 num_C ;
  9 num_C ;

LEXICON 20t40
 %0 num_V ;
  1 num_V ;
  2 num_V ;
  3 num_V ;
  4 num_V ;
  5 num_V ;
  6 num_V ;
  7 num_V ;
  8 num_V ;
  9 num_V ;
   
LEXICON 50t90 
 %0 num_C ;
  1 num_C ;
  2 num_C ;
  3 num_C ;
  4 num_C ;
  5 num_C ;
  6 num_C ;
  7 num_C ;
  8 num_C ;
  9 num_C ;
    
LEXICON 1t9sep
 %0%0 num_V ; ! 100, 200, ..., 900
  %0  1t9 ;   ! 101, 102, ..., 201, ...909
      10t99 ;  



LEXICON num_C
+Num+Abs:%>-%EPENT	indholds-K ;
+Num+Abs: # ;
+Num+Abs+Pl:%>-it	K ;
+Num+Abs+Sg+3Sg:%>-ia	K ;
+Num+Abs+Sg+3Pl:%>-iat	K ;
+Num+Abs+Pl+3Sg:%>-ii	K_plur ;
+Num+Abs+Pl+3Pl:%>-ii	K_plur ;
+Num+Rel:%>-ip	K ;
+Num+Rel+Sg+3Sg:%>-iata	K ;
+Num+Rel+Sg+3Pl:%>-iata	K ;
+Num+Rel+Pl+3Sg:%>-iisa	K_plur ;
+Num+Rel+Pl+3Pl:%>-iisa	K_plur ;
+Num+Lok:%>-imi	LokVb ;
+Num+Lok:%>-ini	LokVb ;
+Num+Lok+Sg+3Sg:%>-iani	LokVb ;
+Num+Lok+Sg+3Pl:%>-ianni	LokVb ;
+Num+Lok+Pl+3Sg:%>-iini	LokVb ;
+Num+Lok+Pl+3Pl:%>-iini	LokVb ;
+Num+Ins:%>-imik	K ;
+Num+Ins:%>-inik	K_plur ;
+Num+Trm:%>-imut	TrmVb ;
+Num+Trm:%>-inut	TrmVb ;
+Num+Abl:%>-imit	AblVb ;
+Num+Abl:%>-imiit	K ;
+Num+Abl:%>-init	AblVb ;
+Num+Aeq:%>-itut	K ;
+Num+Via:%>-ikkut	K ;
+Num+U:%>-iu	IV_voq ;
+Num+NNGUR:%>-inngor	IV ;

LEXICON num_V
+Num+Abs:%>-	indholds-K ;	
+Num+Abs:	# ;	
+Num+Abs+Pl:%>-t	K ;
+Num+Abs+Sg+3Sg:%>-a	K ;
+Num+Abs+Sg+3Pl:%>-at	K ;
+Num+Abs+Pl+3Sg:%>-i	K_plur ;
+Num+Abs+Pl+3Pl:%>-i	K_plur ;

+Num+Rel:%>-p	K ;
+Num+Rel+Sg+3Sg:%>-ata	K ;
+Num+Rel+Sg+3Pl:%>-ata	K ;
+Num+Rel+Pl+3Sg:%>-isa	K_plur ;
+Num+Rel+Pl+3Pl:%>-isa	K_plur ;
+Num+Lok:%>-mi	LokVb ;
+Num+Lok:%>-ni	LokVb ;
+Num+Lok+Sg+3Sg:%>-ani	LokVb ;
+Num+Lok+Sg+3Pl:%>-anni	LokVb ;
+Num+Lok+Pl+3Sg:%>-ini	LokVb ;
+Num+Lok+Pl+3Pl:%>-ini	LokVb ;
+Num+Ins:%>-mik	K ;
+Num+Ins:%>-nik	K_plur ;
+Num+Trm:%>-mut	TrmVb ;
+Num+Trm:%>-nut	TrmVb ;
+Num+Abl:%>-mit	AblVb ;
+Num+Abl:%>-miit	K ;
+Num+Abl:%>-nit	AblVb ;
+Num+Aeq:%>-tut	K ;
+Num+Via:%>-kkut	K ;
+Num+U:%>-u	IV_voq ;
+Num+NNGUR:%>-nngor	IV ;

