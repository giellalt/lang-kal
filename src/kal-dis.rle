# =================================================================== #
# =================================================================== #
#    W E S T   G R E E N L A N D I C   D I S A M B I G U A T O R      #
# =================================================================== #
# =================================================================== #

            # =========================================== #
            #            Short table of contents          #
            # =========================================== #

            # Delimiters
            # Tags and sets
	        # Mapping rules
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 
	      # Global disambiguation - cycles 2
              # Syntactic disambiguation


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";


# ============= #
# Tags and sets #
# ============= #


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

# Tags declared as single-membered LISTs 
# ======================================


# Parts of speech
# ---------------


LIST N = N ;
LIST V = V ;
LIST Part = Part ;
LIST Pron = Pron ;
LIST Adj = Adj ;
LIST Adv = Adv ;
LIST Prop = Prop ;
LIST Interj = Interj ;
LIST Num = Num ;

LIST Sg = Sg ;
LIST Du = Du ;
LIST Pl = Pl ;
LIST Attr = Attr ;

LIST Abs = Abs ;
LIST Rel = Rel ;
LIST Trm = Trm ;
LIST Abl = Abl ;
LIST Lok = Lok ;
LIST Aeq = Aeq ;
LIST Ins = Ins ;
LIST Via = Via ;

LIST Vok = Vok ;

LIST Ind = Ind ;
LIST Int = Int ;
LIST Imp = Imp ;
LIST Opt = Opt ;
LIST Cau = Cau ;
LIST Con = Con ;
LIST Par = Par ;
LIST Inf = Inf ;
LIST InfNeg = InfNeg ;
LIST Ite  = Ite  ;

LIST 1Sg = 1Sg ;
LIST 2Sg = 2Sg ;
LIST 3Sg = 3Sg ;
LIST 4Sg = 4Sg ;
LIST 1Pl = 1Pl ;
LIST 2Pl = 2Pl ;
LIST 3Pl = 3Pl ;
LIST 4Pl = 4Pl ;
LIST 1Du = 1Du ;

LIST Clt = Clt ;


LIST CLB = CLB ; LIST ABBR = ABBR ; 
LIST PUNCT = PUNCT ; LIST LEFT = LEFT ; LIST RIGHT = RIGHT ;


LIST IP = IP ;
LIST KAR = KAR ;
LIST MIU = MIU ;
LIST NEQANNGIT = NEQANNGIT ;
LIST NEQAR = NEQAR ;
LIST NEQARSIMA = NEQARSIMA ;
LIST NEQARSINNA = NEQARSINNA ;
LIST NEQARTAR = NEQARTAR ;
LIST NEQARTARALUAR = NEQARTARALUAR ;
LIST NEQARTASSA = NEQARTASSA ;
LIST NEQARTIP = NEQARTIP ;
LIST NEQASSA = NEQASSA ;
LIST NIANNGEQE = NIANNGEQE ;
LIST NIANNGIT = NIANNGIT ;
LIST NIAQE = NIAQE ;
LIST NIAR = NIAR ;
LIST NIARALUAR = NIARALUAR ;
LIST NIARIAR = NIARIAR ;
LIST NIARLUAR = NIARLUAR ;
LIST NIARPALLAAR = NIARPALLAAR ;
LIST NIARPALLAARNIAR = NIARPALLAARNIAR ;
LIST NIARTAR = NIARTAR ;
LIST NIARTARSIMA = NIARTARSIMA ;
LIST NIARTIP = NIARTIP ;
LIST NIASSA = NIASSA ;
LIST NNGINNIAR = NNGINNIAR ;
LIST NNGIT = NNGIT ;
LIST QANNGIT = QANNGIT ;
LIST QAR = QAR ;
LIST QE = QE ;
LIST SIMA = SIMA ;
LIST SIMANNGIT = SIMANNGIT ;
LIST SIMASASSA = SIMASASSA ;
LIST SIMASINNAA = SIMASINNAA ;
LIST SIMASINNAAGALUAR = SIMASINNAAGALUAR ;
LIST SIMATIP = SIMATIP ;
LIST SINNAA = SINNAA ;
LIST SINNAAGALUAR = SINNAAGALUAR ;
LIST SINNAANIASSA = SINNAANIASSA ;
LIST SINNAANNGIT = SINNAANNGIT ;
LIST SINNAASSA = SINNAASSA ;
LIST SSA = SSA ;
LIST SSANNGIT = SSANNGIT ;
LIST SSASIMA = SSASIMA ;
LIST TAQE = TAQE ;
LIST TAR = TAR ;
LIST TARIAQAR = TARIAQAR ;
LIST TARNEQASSA = TARNEQASSA ;
LIST TARNIAR = TARNIAR ;
LIST TARSIMA = TARSIMA ;
LIST TARSINNAA = TARSINNAA ;
LIST TASSA = TASSA ;
LIST TIP = TIP ;


LIST LU = LU ;
LIST LUGOOQ = LUGOOQ ;
LIST LUMI = LUMI ;
LIST LUMIAASIIT = LUMIAASIIT ;
LIST LI = LI ;
LIST LISSAAQ = LISSAAQ ;
LIST LIGOOQ = LIGOOQ ;
LIST LUUNNIILLI = LUUNNIILLI ;
LIST LUUNNIIT = LUUNNIIT ;
LIST LUUNNIIMMI = LUUNNIIMMI ;
LIST UNA = UNA ;
LIST GOOQ = GOOQ ;
LIST GOORUNA = GOORUNA ;
LIST AASIINNGOOQ = AASIINNGOOQ ;
LIST AASIIT = AASIIT ;
LIST MI = MI ;
LIST MIAASIINNGOOQ = MIAASIINNGOOQ ;
LIST MIAASIIT = MIAASIIT ;
LIST MIGOOQ = MIGOOQ ;
LIST TTAAQ = TTAAQ ;
LIST TTAARLU = TTAARLU ;
LIST MITAAVA = MITAAVA ;
LIST MITAAVAMI = MITAAVAMI ;
LIST MITAAVAAKU = MITAAVAAKU ;
LIST MITAAVAANA = MITAAVAANA ;

LIST SUB = SUB ;


# Syntaktiske klasser

# Alle
LIST WORD = N V Part Prop Num ;

# Verb

SET TRANSVERB = 
(V 1Sg 2Sg) | (V 1Sg 3Sg) | (V 1Sg 4Sg) | (V 2Sg 1Sg) | (V 2Sg 3Sg) |
(V 2Sg 4Sg) | (V 3Sg 1Sg) | (V 3Sg 2Sg) | (V 3Sg 3Sg) | (V 3Sg 4Sg) |
(V 4Sg 1Sg) | (V 4Sg 2Sg) | (V 4Sg 3Sg) | (V 1Sg 2Pl) | (V 1Sg 3Pl) |
(V 1Sg 4Pl) | (V 2Sg 1Pl) | (V 2Sg 3Pl) | (V 2Sg 4Pl) | (V 3Sg 1Pl) |
(V 3Sg 2Pl) | (V 3Sg 3Pl) | (V 3Sg 4Pl) | (V 4Sg 1Pl) | (V 4Sg 2Pl) |
(V 4Sg 3Pl) | (V 1Pl 2Sg) | (V 1Pl 3Sg) | (V 1Pl 4Sg) | (V 2Pl 1Sg) |
(V 2Pl 3Sg) | (V 2Pl 4Sg) | (V 3Pl 1Sg) | (V 3Pl 2Sg) | (V 3Pl 3Sg) |
(V 3Pl 4Sg) | (V 4Pl 1Sg) | (V 4Pl 2Sg) | (V 4Pl 3Sg) | (V 1Pl 2Pl) |
(V 1Pl 3Pl) | (V 1Pl 4Pl) | (V 2Pl 1Pl) | (V 2Pl 3Pl) | (V 2Pl 4Pl) |
(V 3Pl 1Pl) | (V 3Pl 2Pl) | (V 3Pl 3Pl) | (V 3Pl 4Pl) | (V 4Pl 1Pl) |
(V 4Pl 2Pl) | (V 4Pl 3Pl) ;

SET INTRANSVERB = V - TRANSVERB ;

SET PLOBJVERB =
(V 1Sg 2Pl) | (V 1Sg 3Pl) | (V 1Sg 4Pl) | 
(V 2Sg 1Pl) | (V 2Sg 3Pl) | (V 2Sg 4Pl) | 
(V 3Sg 1Pl) | (V 3Sg 2Pl) | (V 3Sg 3Pl) | (V 3Sg 4Pl) | 
(V 4Sg 1Pl) | (V 4Sg 2Pl) | (V 4Sg 3Pl) |
(V 1Pl 2Pl) | (V 1Pl 3Pl) | (V 1Pl 4Pl) | 
(V 2Pl 1Pl) | (V 2Pl 3Pl) | (V 2Pl 4Pl) |
(V 3Pl 1Pl) | (V 3Pl 2Pl) | (V 3Pl 3Pl) | (V 3Pl 4Pl) | 
(V 4Pl 1Pl) | (V 4Pl 2Pl) | (V 4Pl 3Pl) ;

SET SGOBJVERB =
(V 1Sg 2Sg) | (V 1Sg 3Sg) | (V 1Sg 4Sg) | (V 2Sg 1Sg) | (V 2Sg 3Sg) |
(V 2Sg 4Sg) | (V 3Sg 1Sg) | (V 3Sg 2Sg) | (V 3Sg 3Sg) | (V 3Sg 4Sg) |
(V 4Sg 1Sg) | (V 4Sg 2Sg) | (V 4Sg 3Sg) | 
(V 1Pl 2Sg) | (V 1Pl 3Sg) | (V 1Pl 4Sg) | (V 2Pl 1Sg) |
(V 2Pl 3Sg) | (V 2Pl 4Sg) | (V 3Pl 1Sg) | (V 3Pl 2Sg) | (V 3Pl 3Sg) |
(V 3Pl 4Sg) | (V 4Pl 1Sg) | (V 4Pl 2Sg) | (V 4Pl 3Sg) ;

SET 3SGSUBJVERB =
(V 3Sg 1Sg) | (V 3Sg 2Sg) | (V 3Sg 3Sg) | (V 3Sg 4Sg) ;

SET NONIMPVERB = (V Ind) | (V Cau) | (V Int) | (V Par) | (V Ite) | 
                 (V Inf) | (V InfNeg) | (V Opt) | (V Con) | (V Vok) ;

# Leksikalske klasser

# Verb

LIST LOKVERB = "najugaq" ;

# Substantiver

SET KASUS = Abs | Rel | Trm | Abl | Lok | Aeq | Ins | Via ;

SET NOT-PROP = WORD - Prop ;

# Syntaks

LIST @PRED = @PRED ;
LIST @SUBJ = @SUBJ ;
LIST @POSS = @POSS ;
LIST @OBJ = @OBJ ;
LIST @ADVL = @ADVL ;
LIST @INTERJ = @INTERJ ;
LIST @PROP> = @PROP> ;


# ============ #
# ============ #
# Regelsektion #
# ============ #
# ============ #


MAPPINGS


MAP (@PRED) TARGET Ind OR Imp OR Cau OR Int OR Par OR Ite 
		       OR Inf OR InfNeg OR Opt OR Con OR Vok ;

MAP (@ADVL) TARGET Trm OR Abl OR Lok OR Aeq OR Ins OR Via ;

MAP (@SUBJ @OBJ) TARGET Abs ;

MAP (@POSS) TARGET Rel IF (1 N) ;

MAP (@OBJ) TARGET Rel ;

MAP (@ADVL) TARGET Part ;

MAP (@INTERJ) TARGET Interj ;

MAP (@PROP>) TARGET Prop IF (0 Attr);

# Denne tag er for alle ord vi ikke har funktion til.
MAP (@X) TARGET WORD ;


CONSTRAINTS

# One-cohort disambiguation - cycle 0

REMOVE N IF (NOT -1 BOS)(0 Prop) ;
	## Ilalu Nuup eqqaani iliverpassuit.

# Many-cohort disambiguation - cycle 1

# ======== #
# Navneord # 
# ======== #

SELECT Attr IF (0 Prop)(1 (Prop Rel) OR (Prop Abs));

# ==== #
# Fald # 
# ==== #

# Hugseregel:
# subj-abs IV
# subj-rel obj-abs TV.

# Intransitive sætninger
# ====================== #

SELECT (Abs Pl) IF (1 (V 3Pl)) ;
	## Aviisit atuarpavut.

REMOVE Rel IF (1 (V 3Sg))(NOT 1 TRANSVERB);
	## Nuummi najugaqarpoq.

REMOVE (Abs) IF (*1 LOKVERB)(NOT -1 Lok);
	## Nuummi najugaqarpoq.


# Transitive sætninger
# =================== #

	## Kaalip ataataat issiaviit toquppaa.
	#  (N.r-poss N.abs)subj N.r.obj tv


SELECT 3Pl IF (-2 Abs)(-1 (Rel Pl))(0 V);
	## Ippassaq unnukkut filmerput.

REMOVE (Abs) IF (-1 Abs)(0 (N Rel Pl))(1 SGOBJVERB);

SELECT (Rel Pl) IF (-1 Abs)(0 N)(1 (V 3Pl));
	## Ippassaq unnukkut filmerput.

SELECT Sg IF (0C (@SUBJ))(*1C 3SGSUBJVERB);

REMOVE (@OBJ) IF (0 Abs)(*1 TRANSVERB BARRIER INTRANSVERB);
REMOVE (@OBJ) IF (0 Abs)(*-1 TRANSVERB BARRIER INTRANSVERB);

# NP-er

SELECT (Abs Sg) IF (-1 (Abs Sg))(0 LU);
	## Jaakup Juda qatanngutaalu.
# Frisk gjetting.


REMOVE (Abs Sg 1Sg) IF (-1 (Rel Sg)) ;
	## Daavip ernera.
	

# Verbal disambiguering
# =====================

REMOVE Imp IF (0 N OR Adj)(1 NONIMPVERB);
	## Taamaattumik tamatta suleqatigiittariaqarpugut.
	

# Syntaktisk disambiguering
# ========================= #

SELECT (@SUBJ) IF (1C @OBJ)(2 TRANSVERB) ;
	## Nanoruna piniartup toqukkaa.
