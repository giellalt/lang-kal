! ==================================================== !
! Fil for at generere de morfofonologiske reglerne til !
!             vor grønlandske analysator               !
! ==================================================== !

echo << Vi definerer vokalerne... >>

define Vow [ a | e | i | o | u | y | æ | ø | å |
             A | E | I | O | U | Y | Æ | Ø | Å |
                i2 | i3 ] ;
 
echo << Vi definerer konsonanterne... >>
 
define Cns [ t2 | b | c | d | f | g | h | j | k | l | m |
             n | p | q | r | s | t | v | w | x | z |
             B | C | D | F | G | H | J | K | L | M |
             N | P | Q | R | S | T | V | W | X | Z   ]  ;
 
echo << Vi definerer sæt af segmenter... >>
define Segment [ Cns | Vow ] ;
define Uvular [ q | r ] ;
define NonUvular [ Cns - Uvular ] ;
 
echo << Vi definerer Dummy-symboler >>
define Dummy [ %> | %< | %TRUNC | %CLIT | %STRUNC | %POSSESSORTRUNC | %PRONTRUNC | %ROD ] ;

echo << Vi definerer de ikke fonetisk betingede allomorfer >>
define InfinitivLu [ l -> l l || [ a | u | i ] %> _ ] ;
define IndikativVu [ v -> p || [ p | r ] %> _ [ u | i | a | o ] ] ;
define Possessortrunc [ i t -> i || [ u | i ] %> %POSSESSORTRUNC _ ] ;
define Prontrunc [ s -> 0 || u %> %PRONTRUNC _ ] ;
define Pronvar1 [ u -> 0 || a %ROD _ u n ] ;

echo << Vi definerer morfofonemiske regler >>
echo << Nogle arkifonemer >>
define TVow [ t2 -> s || Vow ( \Cns ) %> _ ] ; 
define TCns [ t2 -> t || Cns ( \Vow ) %> _ ] ;
       ! "ikke Cns" og "ikke Vow" for at tillade flags imellem

define xOprydning [ x -> 0 || Cns %> _ ] ;
define xpAssimilation [ x -> p || [ a | i | u ] %> _ p ] ;
define xmAssimilation [ x -> m || [ a | i | u ] %> _ m ] ;

! Assimilationsrægler
! - - - - - - - - - - 

echo << Vi definerer regressiv assimilation >>
define mAssimilation [ NonUvular -> m || Vow _ %> m ] ; ! panik > panimmik
define nAssimilation [ NonUvular -> n || Vow _ %> n ] ; ! panik > paninnik
 define kAssimilation [ NonUvular -> k || Vow _ %> k ] ; 
define lAssimilation [ NonUvular -> l || Vow _ %> l ] ; 
define pAssimilation [ NonUvular -> p || Vow _ %> p ] ; 
define rAssimilation [ NonUvular -> r || Vow _ %> r ] ; 
define sAssimilation [ NonUvular -> s || Vow _ %> s ] ; 
define tAssimilation [ NonUvular -> t || Vow _ %> t ] ; 
define vAssimilation [ NonUvular -> v || Vow _ %> v ] ; 
define qAssimilation [ NonUvular -> q || Vow _ %> q ] ; 
define gAssimilation [ g -> k || [p | k ] %> _ ] ;

echo << Vi definerer så en del andre rægler >>

define qgFusion [ [ r | q ] (\Segment) (\Segment) g -> r ] ;
define frikativisering [ q -> r || \%TRUNC _ [ %> Segment | %CLIT ] ] ;
define trunkering [ Cns -> 0 || _ %> %TRUNC ] ; 
define TclitisU [ t -> n || _ %CLIT [ u | a ] ] ;
define Tclitisg1 [ [ t | p | k ] -> n || _ %CLIT g ] ;
define Tclitisg2 [ g -> n g || n %CLIT _ ] ;
define clitisLAssimilation [ Cns -> l || _ %CLIT l ] ;
define clitisMAssimilation [ Cns -> m || _ %CLIT m ] ;
define clitisVAssimilation [ Vow -> a || a %CLIT _ ] ;
define clitisQFrikativisering [ q -> r || _ %CLIT [ m | l ] ] ;
define clitisNasalisering [ k -> n g || _ %CLIT Vow ] ;
define clitis3cons [ NonUvular -> 0 || _ %CLIT t t ] ;
define aasiit1Vokal [ a -> 0 || _ %CLIT a a ] ;
define aasiit2Vokal [ a ( %> ) ( %TRUNC ) a -> a a j || _ %CLIT a a ] ;
define Sgeminering [ a [ q | k ] -> s s a ||Vow _ %> %STRUNC ] ;

echo << Vi definerer vokalregler >>
define schwaC [ [ i2 | i3 ] -> i || _ %> ( %TRUNC ) Cns ] ;
define schwaV [ [ i2 | i3 ] -> a || _ %> ( %TRUNC ) Vow ] ;
define schwaNULL [ i3 -> 0 || t _ [ .#. | %CLIT ] ] ;
define radikalisering [ i -> e, u -> o || _ ( %> ) ( %TRUNC ) Uvular ] ;
define eafRadikalisering [ e -> i || _ ( %> ) ( %TRUNC ) [ NonUvular | i | u | a ] ] ;
define oafRadikalisering [ o -> u || _ ( %> ) ( %TRUNC ) [ NonUvular | i | u | a ] ] ;
define progressivVassimilering [ Vow -> a || a [ %> | %ROD ] ( %TRUNC ) _ ] ; 
                                                                    
echo << Vi rydder op >>

define IngenArkefonemer [ i2 -> i, i3 -> i, t2 -> t ] ;
define DummyDeletion [ Dummy -> 0 ] ;
define vSlet [ v -> 0 || u _ [ i | e | a ] ] ; !En rent ortografisk regel

echo << Vi sætter ting sammen >>
                                                                      
read regex [
TCns	![ t2 -> t || Cns ( \Vow ) %> _ ]
.o.
TVow	![ t2 -> s || Vow ( \Cns ) %> _ ]
.o.
InfinitivLu
.o.
IndikativVu
.o.
Possessortrunc
.o.
Prontrunc
.o.
Pronvar1
.o.
xOprydning
.o.
xpAssimilation
.o.
xmAssimilation
.o.
frikativisering 
.o.
qgFusion
.o.
gAssimilation
.o.
mAssimilation 
.o.
nAssimilation 
.o.
kAssimilation
.o.
lAssimilation
.o.
pAssimilation
.o.
rAssimilation
.o.
sAssimilation
.o.
tAssimilation
.o.
vAssimilation
.o.
qAssimilation
.o.
trunkering
.o.
schwaC
.o.
schwaV
.o.
schwaNULL
.o.
Sgeminering
.o.
radikalisering
.o.
eafRadikalisering
.o.
oafRadikalisering
.o.
progressivVassimilering	![ Vow -> a || a [ %> | %ROD ] ( %TRUNC ) _ ]
.o.
TclitisU
.o.
Tclitisg1
.o.
Tclitisg2
.o.
clitisLAssimilation
.o.
clitisMAssimilation
.o.
clitisVAssimilation
.o.
clitisQFrikativisering
.o.
clitis3cons
.o.
clitisNasalisering
.o.
aasiit2Vokal
.o.
aasiit1Vokal
.o.
IngenArkefonemer
.o.
DummyDeletion
.o.
vSlet ] ;
