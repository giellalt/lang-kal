! ==================================================== !
! Fil for at generere de morfofonologiske reglerne til !
!             vor grønlandske analysator               !
! ==================================================== !

echo << Vi definerer vokalerne... >>

define Vow [ a | e | i | o | u | y | æ | ø | å |
             A | E | I | O | U | Y | Æ | Ø | Å |
                i2 | i3 ] ;
 
echo << Vi definerer konsonanterne... >>
 
define Cns [ t2 | b | c | d | f | g | h | j | k | l | m |
             n | p | q | r | s | t | v | w | x | z |
             B | C | D | F | G | H | J | K | L | M |
             N | P | Q | R | S | T | V | W | X | Z   ]  ;
 
echo << Vi definerer sæt af segmenter... >>
define Segment [ Cns | Vow ] ;
define Uvular [ q | r ] ;
define NonUvular [ Cns - Uvular ] ;
 
echo << Vi definerer Dummy-symboler >>
define Dummy [ %> | %< | %^M | %CLIT ] ;
 
echo << Vi definerer morfofonemiske regler >>
echo << Nogle arkifonemer >>
define TVow [ t2 -> s || Vow ( \Cns ) %> _ ] ; 
define TCns [ t2 -> t || Cns ( \Vow ) %> _ ] ;
       ! "ikke Cns" og "ikke Vow" for at tillade flags imellem
define InfinitivLu [ l -> l l || [ a | u | i ] %> _ u ] ;
define IndikativVu [ v -> p || [ p | r ] %> _ [ u | i | a ] ] ;
define xOprydning [ x -> 0 || Cns %> _ ] ;
define xpAssimilation [ x -> p || [ a | i | u ] %> _ p ] ;
define xmAssimilation [ x -> m || [ a | i | u ] %> _ m ] ;

! Assimilationsrægler
! - - - - - - - - - - 

echo << Vi definerer regressiv assimilation >>
define mAssimilation [ NonUvular -> m || Vow _ %> m ] ; ! panik > panimmik
define nAssimilation [ NonUvular -> n || Vow _ %> n ] ; ! panik > paninnik
 define kAssimilation [ NonUvular -> k || Vow _ %> k ] ; 
define lAssimilation [ NonUvular -> l || Vow _ %> l ] ; 
define pAssimilation [ NonUvular -> p || Vow _ %> p ] ; 
define rAssimilation [ NonUvular -> r || Vow _ %> r ] ; 
define sAssimilation [ NonUvular -> s || Vow _ %> s ] ; 
define tAssimilation [ NonUvular -> t || Vow _ %> t ] ; 
define vAssimilation [ NonUvular -> v || Vow _ %> v ] ; 
define qAssimilation [ NonUvular -> q || Vow _ %> q ] ; 
define gAssimilation [ g -> k || [p | k ] %> _ ] ;

echo << Vi definerer så en del andre rægler >>

define qgFusion [ [ r | q ] %> g -> r ] ;
define frikativisering [ q -> r ||  \%^M  _ %> Segment ] ; !ikke ved trunkative morfemer på q
define trunkering [ Cns -> 0 || _ %> %^M ] ; 
define TclitisU [ t -> n || _ %CLIT u ] ;
define Tclitisg1 [ [ t | p | k ] -> n || _ %CLIT g ] ;
define Tclitisg2 [ g -> n g || n %CLIT _ ] ;
define ttaaqClitis [ t t -> t || Cns %CLIT _ a ] ;
define clitisAssimilation [ Cns -> l || _ %CLIT l ] ;
define clitisVAssimilation [ Vow -> a || a %CLIT _ ] ;

echo << Vi definerer vokalregler >>
define schwaC [ [ i2 | i3 ] -> i || _ %> ( %^M ) Cns ] ;
define schwaV [ [ i2 | i3 ] -> a || _ %> ( %^M ) Vow ] ;
define schwaNULL [ i3 -> 0 || t _ [ .#. | %CLIT ] ] ;
define radikalisering [ i -> e, u -> o || _ ( %> ) ( %^M ) Uvular ] ;
define progressivVassimilering [ Vow -> a || a %> ( %^M ) _ ] ; 
                                                                    
echo << Vi rydder op >>

define IngenArkefonemer [ i2 -> i, i3 -> i, t2 -> t ] ;
define DummyDeletion [ Dummy -> 0 ] ;

echo << Vi sætter ting sammen >>
                                                                      
read regex [
TCns
.o.
TVow
.o.
InfinitivLu
.o.
IndikativVu
.o.
xOprydning
.o.
xpAssimilation
.o.
xmAssimilation
.o.
qgFusion
.o.
gAssimilation
.o.
mAssimilation 
.o.
nAssimilation 
.o.
kAssimilation
.o.
lAssimilation
.o.
pAssimilation
.o.
rAssimilation
.o.
sAssimilation
.o.
tAssimilation
.o.
vAssimilation
.o.
qAssimilation
.o.
frikativisering 
.o.
trunkering
.o.
schwaC
.o.
schwaV
.o.
schwaNULL
.o.
radikalisering
.o.
progressivVassimilering
.o.
TclitisU
.o.
Tclitisg1
.o.
Tclitisg2
.o.
ttaaqClitis
.o.
clitisAssimilation
.o.
clitisVAssimilation
.o.
IngenArkefonemer
.o.
DummyDeletion ] ;
