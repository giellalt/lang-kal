! ==================================================== !
! Fil for at generere de morfofonologiske reglerne til !
!             vor grønlandske analysator               !
! ==================================================== !

echo << Vi definerer vokalerne... >>

define Vow [ a | e | i | o | u | y | æ | ø | å |
             A | E | I | O | U | Y | Æ | Ø | Å |
                i2 | i3 ] ;
 
echo << Vi definerer konsonanterne... >>
 
define Cns [ t2 | b | c | d | f | g | h | j | k | l | m |
             n | p | q | r | s | t | v | w | x | z |
             B | C | D | F | G | H | J | K | L | M |
             N | P | Q | R | S | T | V | W | X | Z   ]  ;
 
echo << Vi definerer sæt af segmenter... >>
define Segment [ Cns | Vow ] ;
define Uvular [ q | r ] ;
define NonUvular [ Cns - Uvular ] ;
 
echo << Vi definerer Dummy-symboler >>
define Dummy [ %> | %< | %^M | %CLIT ] ;
 
! Assimilationsrægler
! - - - - - - - - - - 

echo << Nogle arkifonemer >>
define TVow [ t2 -> s || Vow %> _ ] ;
define TCns [ t2 -> t || Cns %> _ ] ;
 
define IndikativVu [ v -> p || Cns %> _ u ] ; 
echo << Vi definerer regressiv assimilation >>
define mAssimilation [ NonUvular -> m || Vow _ %> m ] ; ! panik > panimmik
define nAssimilation [ NonUvular -> n || Vow _ %> n ] ; ! panik > paninnik
 define kAssimilation [ NonUvular -> k || Vow _ %> k ] ; 
define lAssimilation [ NonUvular -> l || Vow _ %> l ] ; 
define pAssimilation [ NonUvular -> p || Vow _ %> p ] ; 
define rAssimilation [ NonUvular -> r || Vow _ %> r ] ; 
define sAssimilation [ NonUvular -> s || Vow _ %> s ] ; 
define tAssimilation [ NonUvular -> t || Vow _ %> t ] ; 
define vAssimilation [ NonUvular -> v || Vow _ %> v ] ; 
define qAssimilation [ NonUvular -> q || Vow _ %> q ] ; 

echo << Vi definerer så en del andre rægler >>

define qgFusion [ q %> g -> r ] ;
define frikativisering [ q -> r || _ %> Segment ] ;
define trunkering [ Cns -> 0 || _ %> %^M ] ; 
 
echo << Vi definerer vokalregler >>
define schwaC [ [ i2 | i3 ] -> i || _ %> ( %^M ) Cns ] ;
define schwaV [ [ i2 | i3 ] -> a || _ %> ( %^M ) Vow ] ;
define schwaNULL [ i3 -> 0 || t _ [ .#. | %CLIT ] ] ;
define radikalisering [ i -> e, u -> o || _ ( %> ) ( %^M ) Uvular ] ;
define progressivVassimilering [ Vow -> a || a %> ( %^M ) _ ] ; 
                                                                    
echo << Vi rydder op >>
                                                                      
define DummyDeletion [ Dummy -> 0 ] ;

echo << Vi sætter ting sammen >>
                                                                      
read regex [
TCns
.o.
TVow
.o.
IndikativVu
.o.
mAssimilation 
.o.
nAssimilation 
.o.
kAssimilation
.o.
lAssimilation
.o.
pAssimilation
.o.
rAssimilation
.o.
sAssimilation
.o.
tAssimilation
.o.
vAssimilation
.o.
qAssimilation
.o.
qgFusion
.o.
frikativisering 
.o.
trunkering
.o.
schwaC
.o.
schwaV
.o.
schwaNULL
.o.
radikalisering
.o.
progressivVassimilering
.o.
DummyDeletion ] ;
