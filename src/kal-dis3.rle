# =================================================================== #
# =================================================================== #
#     W E S T   G R E E N L A N D I C   D I S A M B I G U A T O R     #
# =================================================================== #
# =================================================================== #

            # =========================================== #
            #            Short table of contents          #
            # =========================================== #

            # Delimiters
            # Tags and sets
	        # Mapping rules
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 
	      # Global disambiguation - cycles 2
              # Syntactic disambiguation


# ========== #
# Delimiters #
# ========== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";


# ============= #
# Tags and sets #
# ============= #

# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

# Tags declared as single-membered LISTs 
# ======================================

# Parts of speech
# ---------------

LIST N = N ;
LIST V = V ;
LIST Part = Part ;
LIST Pron = Pron ;
LIST Adj = Adj ;
LIST Adv = Adv ;
LIST Prop = Prop ;
LIST Interj = Interj ;
LIST Num = Num ;

LIST Sg = Sg ;
LIST Du = Du ;
LIST Pl = Pl ;
LIST Attr = Attr ;

LIST nn = nn ;
LIST vv = vv ;
LIST nv = nv ;
LIST vn = vn ;

LIST Abs = Abs ;
LIST Rel = Rel ;
LIST Trm = Trm ;
LIST Abl = Abl ;
LIST Lok = Lok ;
LIST Aeq = Aeq ;
LIST Ins = Ins ;
LIST Via = Via ;

LIST Vok = Vok ;

LIST Ind = Ind ;
LIST Int = Int ;
LIST Imp = Imp ;
LIST Opt = Opt ;

LIST Cau = Cau ;
LIST Con = Con ;
LIST Par = Par ;
LIST Inf = Inf ;
LIST InfNeg = InfNeg ;
LIST Ite  = Ite  ;

LIST 1Sg = 1Sg ;
LIST 2Sg = 2Sg ;
LIST 3Sg = 3Sg ;
LIST 4Sg = 4Sg ;
LIST 1Pl = 1Pl ;
LIST 2Pl = 2Pl ;
LIST 3Pl = 3Pl ;
LIST 4Pl = 4Pl ;
LIST 1Du = 1Du ;
LIST 2Du = 2Du ;
LIST 3Du = 3Du ;
LIST 4Du = 4Du ;

LIST 1SgO = 1SgO ;
LIST 2SgO = 2SgO ;
LIST 3SgO = 3SgO ;
LIST 4SgO = 4SgO ;
LIST 1PlO = 1PlO ;
LIST 2PlO = 2PlO ;
LIST 3PlO = 3PlO ;
LIST 4PlO = 4PlO ;
LIST 1DuO = 1DuO ;
LIST 2DuO = 2DuO ;
LIST 3DuO = 3DuO ;
LIST 4DuO = 4DuO ;


LIST Clt = Clt ;


LIST CLB = CLB ; 
LIST Abbr = Abbr ; 
LIST PUNCT = PUNCT ; 
LIST LEFT = LEFT ; 
LIST RIGHT = RIGHT ;

LIST COMMONAFFSTRING = \= ;

LIST IP = IP ;
LIST KAR = KAR ;
LIST MIU = MIU ;
LIST NEQANNGIT = NEQANNGIT ;
LIST NEQAR = NEQAR ;
LIST NEQARSIMA = NEQARSIMA ;
LIST NEQARSINNA = NEQARSINNA ;
LIST NEQARTAR = NEQARTAR ;
LIST NEQARTARALUAR = NEQARTARALUAR ;
LIST NEQARTASSA = NEQARTASSA ;
LIST NEQARTIP = NEQARTIP ;
LIST NEQASSA = NEQASSA ;
LIST NIANNGEQE = NIANNGEQE ;
LIST NIANNGIT = NIANNGIT ;
LIST NIAQE = NIAQE ;
LIST NIAR = NIAR ;
LIST NIARALUAR = NIARALUAR ;
LIST NIARIAR = NIARIAR ;
LIST NIARLUAR = NIARLUAR ;
LIST NIARPALLAAR = NIARPALLAAR ;
LIST NIARPALLAARNIAR = NIARPALLAARNIAR ;
LIST NIARTAR = NIARTAR ;
LIST NIARTARSIMA = NIARTARSIMA ;
LIST NIARTIP = NIARTIP ;
LIST NIASSA = NIASSA ;
LIST NNGINNIAR = NNGINNIAR ;
LIST NNGIT = NNGIT ;
LIST QANNGIT = QANNGIT ;
LIST QAR = QAR ;
LIST QE = QE ;
LIST QQU = QQU ;
LIST SAR = SAR ;
LIST SIMA = SIMA ;
LIST SIMANNGIT = SIMANNGIT ;
LIST SIMASASSA = SIMASASSA ;
LIST SIMASINNAA = SIMASINNAA ;
LIST SIMASINNAAGALUAR = SIMASINNAAGALUAR ;
LIST SIMATIP = SIMATIP ;
LIST SINNAA = SINNAA ;
LIST SINNAAGALUAR = SINNAAGALUAR ;
LIST SINNAANIASSA = SINNAANIASSA ;
LIST SINNAANNGIT = SINNAANNGIT ;
LIST SINNAASSA = SINNAASSA ;
LIST SSA = SSA ;
LIST SSANNGIT = SSANNGIT ;
LIST SSASIMA = SSASIMA ;
LIST TAQE = TAQE ;
LIST TAR = TAR ;
LIST TARIAQAR = TARIAQAR ;
LIST TARNEQASSA = TARNEQASSA ;
LIST TARNIAR = TARNIAR ;
LIST TARSIMA = TARSIMA ;
LIST TARSINNAA = TARSINNAA ;
LIST TASSA = TASSA ;
LIST TIP = TIP ;
LIST KKUT = KKUT ;

LIST LU = LU ;
LIST LUGOOQ = LUGOOQ ;
LIST LUMI = LUMI ;
LIST LUMIAASIIT = LUMIAASIIT ;
LIST LI = LI ;
LIST LISSAAQ = LISSAAQ ;
LIST LIGOOQ = LIGOOQ ;
LIST LUUNNIILLI = LUUNNIILLI ;
LIST LUUNNIIT = LUUNNIIT ;
LIST LUUNNIIMMI = LUUNNIIMMI ;
LIST UNA = UNA ;
LIST GOOQ = GOOQ ;
LIST GOORUNA = GOORUNA ;
LIST AASIINNGOOQ = AASIINNGOOQ ;
LIST AASIIT = AASIIT ;
LIST MI = MI ;
LIST MIAASIINNGOOQ = MIAASIINNGOOQ ;
LIST MIAASIIT = MIAASIIT ;
LIST MIGOOQ = MIGOOQ ;
LIST TTAAQ = TTAAQ ;
LIST TTAARLU = TTAARLU ;
LIST MITAAVA = MITAAVA ;
LIST MITAAVAMI = MITAAVAMI ;
LIST MITAAVAAKU = MITAAVAAKU ;
LIST MITAAVAANA = MITAAVAANA ;

LIST SUB = SUB ;



# Syntaktiske klasser
# ===================

# Alle ordklasser
# ---------------

LIST WORD = N V Adj Pron Adv Interj Part Num ;

# Verb
# ----

SET VFIN = Ind | Opt | Int | Imp ;

SET V-NOT-Par = V - Par ;


SET TRANSVERB = 1SgO | 2SgO | 3SgO | 4SgO | 1DuO | 2DuO | 3DuO | 4DuO | 1PlO | 2PlO | 3PlO | 4PlO ; 
SET INTRANSVERB = V - TRANSVERB ;
SET DOTR = QQU | TIP | SAR ;


SET SGOBJVERB =  1SgO | 2SgO | 3SgO | 4SgO ;
SET PLOBJVERB =  1PlO | 2PlO | 3PlO | 4PlO ;

SET 3SGSUBJVERB = (V 3Sg) ;
SET 3PLSUBJVERB = (V 3Pl) ;

SET 34SGOBJVERB = 3SgO ;
SET 34PLOBJVERB = 3PlO ;

SET VERBNOT3SGSUBJ = V - 3SGSUBJVERB ;
SET VERBNOT3PLSUBJ = V - 3PLSUBJVERB ;

SET 12SUBJVERB = (V 1Sg) | (V 1Du) | (V 1Pl) | (V 2Sg) | (V 2Du) | (V 2Pl) ;
  
SET NONIMPVERB = (V Ind) | (V Cau) | (V Int) | (V Par) | (V Ite) | 
                 (V Inf) | (V InfNeg) | (V Opt) | (V Con) | (V Vok) ;

# Substantiver
# ------------

SET POSSESSUM3 = (N 3Sg) | (N 3Pl) ; #Pers definition 0608

SET POSSESSUM4 = (N 4Sg) | (N 4Pl) ; #Pers definition 0608

SET POSSESSUM = (N 1Sg) |  (N 2Sg) |  (N 3Sg) |  (N 1Pl) |  (N 2Pl) |  (N 3Pl) ; 


# Leksikalske klasser
# ===================

# Verb
# ----

LIST LOKVERB = "najugaq" "najugaqar" "ilinniar" ;

LIST ABLVERB = "ani" ;

LIST TRMVERB = "iser" ;

SET MOVE-VERB = LOKVERB | ABLVERB | TRMVERB ;

LIST ORATIOOBLVERB = "isuma" "nalu" "oqar" "qular"  "qularnar" "taku" ;

LIST VEXCLAM = "sunaaffa" "uffa" "aali" "naak" ;

LIST humverb = "suli" ; # Verb der vil ha humane subj, og ikke er i andre set

SET HUMVERB = humverb OR ORATIOOBLVERB ; # alle verb  der vil ha humane subj

LIST PSEUDO-HTR = "neri" ;



# Substantiver
# ------------

LIST HUMAN = "illoq" ;

LIST PLACE = "illu" "Kaffi" ;
# Remove Kaffi when tagged in lex
LIST FAM = "illoq" ;

SET KASUS = Abs | Rel | Trm | Abl | Lok | Aeq | Ins | Via ;
SET NOT-REL = KASUS - Rel ;
SET OBLIQUE =  Trm | Abl | Lok | Aeq | Ins | Via ;

LIST EDIBLE = "neqi" ;
LIST DRINKABLE = "kaffi" ;

SET THING = EDIBLE | DRINKABLE ;
LIST INSTRUMENT = "alussaat" ;

LIST TIME = "sapaat" ;

# Komplementære set

SET NOT-PROP = WORD - Prop ;

SET NOT-Abbr = WORD - Abbr ;

# Proprier
# Desse skal vi hente frå leksikon

LIST Mal = "Trond" "Per" ;
LIST Fem = "Kirsten" ;
LIST Sur = "Rasmussen" "Trosterud" ;

SET FIRSTNAME = Mal | Fem | ("Nuka") ;
SET NAME = Mal | Fem | Sur ;
SET NOT-FIRST = WORD - FIRSTNAME ;


# Syntaks
# =======

LIST @PRED = @PRED ; 		 # predikat
LIST @SUBJ> = @SUBJ> ;		 # subjekt med pred t.h.
LIST @<SUBJ = @<SUBJ ;		 # subjekt med pred t.v.
LIST @OBJ> = @OBJ> ;		 # objekt med pred t.h.
LIST @<OBJ = @<OBJ ;		 # objekt med pred t.v.
LIST @ADVL> = @ADVL> ;		 # adverbial med kerne t.h.
LIST @<ADVL = @<ADVL ;		 # adverbal med kerne t.v.
LIST @CL-ADVL> = @CL-ADVL> ;     # neksusadverbial der modificerer t.h. 
LIST @CL-<ADVL = @CL-<ADVL ;     # neksusadverbial der modificerer t.v.
LIST @CL-CIT> = @CL-CIT> ;	 # predikat i oo der står til et matriseverb t.h. CL-OBJ
LIST @CL-<CIT = @CL-<CIT ;	 # predikat i oo der står til et matriseverb t.v. 
LIST @i-SUBJ> = @i-SUBJ> ;       # subjekt der står til inkorporeret dotr verbal t.h.
LIST @i-<SUBJ = @i-<SUBJ ;       # subjekt der står til inkorporeret dotr verbal t.v.
LIST @i-OBJ> = @i-OBJ> ;         # objekt der står til inkorporeret verbal t.h.
LIST @i-<OBJ = @i-<OBJ ;	 # objekt der står til inkorporeret verbal t.v.
LIST @i-ADVL> = @i-ADVL> ;	 # adverbial der står til inkorporeret verbal t.h.
LIST @i-<ADVL = @i-<ADVL ;	 # adverbial der står til inkorporeret verbal t.v.
LIST @INTERJ = @INTERJ ;	 # interjektion
LIST @PROP> = @PROP> ;		 # proprium til venstre i propriumskede
LIST @HNOUN = @HNOUN ;		 # løst nomen uden predikat
LIST @>N = @>N ;		 # prenominalt adled (slagter Hansen)
LIST @N< = @N< ;	    	 # postnominalt adled
LIST @i->N = @i->N ;		 # adled der står til inkorporeret nominal t.h.
LIST @i-N< = @i-N< ;		 # adled der står til inkorporeret nominal t.v.
LIST @POSS = @POSS ;		 # possessor
LIST @>V = @>V ;                 # adled til verbum # asaneq ajorpara.
LIST @X = @X ;                   # dette ord har endnu ikke fået en mapping-regel

#Per Trond.trm butikken.abs passe-ba.
#su  O         iO       i     y   => O er iS viss vi har iO <== (O kan passiveres)
#su  iS        iO       i     y   => iS er O når vi har iO
#S   A         O        PRED = syntaks modulo kasus

LIST @ARG> = @SUBJ> @OBJ> @ADVL> @CL-ADVL> @CL-CIT> @>V ; # Disse vil ha V t.h.
LIST @<ARG = @<SUBJ @<OBJ @<ADVL @CL-<ADVL @CL-<CIT ;

LIST @ALLOBJ> = @OBJ> @i-OBJ> ;
LIST @<ALLOBJ = @<OBJ @i-<OBJ ;

LIST @ALLSUBJ> = @SUBJ> @i-SUBJ> ;
LIST @<ALLSUBJ = @<SUBJ @i-<SUBJ ;

 # Kladdenotatar, skal slettast
 # SU: abs(iv), rel(tv), trm(dotv), ins; 4subm
 # DO:  abs(tv,ditv), ins(htv); 4subm
 # IO: trm(ditv,dotv)
 # iv:   subj:abs
 # htv:  subj:abs obj:ins
 # tv:   sub:rel obj:abs 
 # Evap      panini         Jensimut    paareqquaa.
 # Evap      pania          Jensimut    paareqquaa.
 # Evap      pania          Jensimut    paareqquaa.
 # Evap      Paninnguaq     Jensimut    paareqquaa.
 # @SUBJ     @OBJ           @OBJ/@ADVL     "Eva bad jens passe Paninnguaq"
 #           @iOBJ          @IOBJ/@iSUBJ          
 # Evap      Jensj        oqarfigaa panini     paareqqullugu
 # Evap      Jensi        oqarfigaa pania      paareqqullugu           
 # Evap      Jensi        oqarfigaa Paninnguaq paareqqullugu
 # subj       obj        pred      o
 # Eva bad Jens passe sin datter
 #         Jensi      iserpoq
 #         Jensi      iserpoq
 #         Evap Jensi iseqquaa.
 #         
 #         tanna tassa tassaavoq
 #         Jensi tasaavoq Olip ernera.
 #         Hansi tassa 
         
 ###     Eva      bad            Jens      passe    Paninnguaq
 # dotv: subj:rel så             obj:trm   [cl-oa   <obj:abs]
 #       subj:rel fik til at     [subj:trm  cl-obj  <obj:abs]
 # 	    subj:rel lovede         iobj:trm  [cl-obj  <obj:abs]
 # ditv: Sonja     gav hæsten   sukker
 #       subj:rel      iobj:trm obj:abs


# ============ #
# ============ #
# Regelsektion #
# ============ #
# ============ #


MAPPINGS

ADD (@N<) TARGET N IF (-1 N)(NEGATE 0 (NEQ)) ;
ADD (@N<) TARGET N IF (-1 N)(0 (NEQ)) ;

MAP (@PRED) TARGET Ind OR Imp OR Int OR Opt ;

ADD (@CL-ADVL> @CL-<ADVL) TARGET Ite OR Cau OR Inf OR InfNeg OR Con OR Par ;
ADD (@PRED) TARGET Inf OR InfNeg IF (0 LU OR LUUNNIIT)(*1 VFIN);
ADD (@PRED) TARGET Inf OR InfNeg IF (0 LU OR LUUNNIIT)(*-1 VFIN);
	## Trond atuarpoq allallunilu.
	
MAP (@CL-CIT> @CL-<CIT) TARGET Par OR Inf OR InfNeg ;

MAP (@i-SUBJ>) TARGET Trm IF (*1 DOTR);
MAP (@i-OBJ>)  TARGET Trm IF (*1 DOTR)(NEGATE 0 TIME);

MAP (@ADVL> @<ADVL) TARGET Trm OR Abl OR Lok OR Aeq OR Via ;


ADD (@OBJ>) TARGET Ins IF (*1 PSEUDO-HTR);
MAP (@ADVL> @<ADVL) TARGET Ins ;

MAP (@SUBJ>) TARGET (Abs Sg) IF (*-1 BOS BARRIER N)(NEGATE 0 TIME)(1 INTRANSVERB + Ind + 3Sg);
MAP (@SUBJ>) TARGET (Abs Pl) IF (*-1 BOS BARRIER N)(NEGATE 0 TIME)(1 INTRANSVERB + Ind + 3Pl);

MAP (@SUBJ> @<SUBJ @<OBJ @OBJ> @HNOUN) TARGET Abs ;
MAP (@POSS @SUBJ> @<SUBJ) TARGET Rel ;

MAP (@ADVL>) TARGET Part ;

MAP (@INTERJ) TARGET Interj ;

MAP (@PROP>) TARGET Prop IF (1 Prop);

MAP (@PROP>) TARGET Abbr IF (*1 Prop BARRIER NOT-Abbr) ;


 # Denne tag er for alle ord vi ikke har funktion til.

MAP (@X) TARGET WORD ;


# ================== #
# ================== #
      CONSTRAINTS
# ================== #
# ================== #

# Tidlige syntaktiske regler


REMOVE @ARG> IF (NEGATE *1 V) ;
REMOVE @<ARG IF (NEGATE *-1 V) ;

REMOVE @ARG> IF (*1C @<ARG BARRIER V);
REMOVE @<ARG IF (*-1C @ARG> BARRIER V);


# One-cohort disambiguation - cycle 0

SELECT Prop IF (NEGATE -1 BOS)(0 Prop) ;
	## Ilalu Nuup eqqaani iliverpassuit.



	
REMOVE ("asap" Inf) IF (NEGATE *-1 ("nateq")) ; #Pers regel 0608 
REMOVE ("kaa" Interj) IF (0 ("Kaali"));

SELECT ("Kina") IF (-1 BOS)(NEGATE *1 ("?")); #BOS = Beginning of sentence
SELECT ("kina") IF (-1 BOS)(*1 ("?"));
	## Kina aniva.


# Many-cohort disambiguation - cycle 1

# ======== #
# Navneord # 
# ======== #

SELECT Prop IF (NEGATE -1 BOS);

SELECT Attr IF (0 Prop)(1 (Prop Rel) OR (Prop Abs));

REMOVE (Prop Attr) IF (NEGATE 1 Prop);

	# Et unikt verb
SELECT Ind IF (NEGATE *-1 Ind)(NEGATE *1 Ind OR ("?"));

REMOVE Attr IF (0 Prop)(NEGATE 1 Prop);


REMOVE N IF (0 ("Nuuk" Prop Lok));
	## Nuummi.

	# Et unikt verb
SELECT Ind IF (NEGATE *-1 Ind)(NEGATE *1 Ind OR ("?"));


REMOVE @POSS IF (NEGATE *1 POSSESSUM3) ; #Pers tilfoejelse 0608

# Part


REMOVE Interj IF (0 Part)(1 (@HNOUN));
	## Tassa illu.

# Appositioner
# ------------

SELECT @N< IF (*-1 Abs BARRIER NOT-REL)(0 Abs)(NEGATE 0 Prop);
SELECT @N< IF (*-1 Lok BARRIER NOT-REL)(0 Lok)(NEGATE 0 Prop);
SELECT @N< IF (*-1 Trm BARRIER NOT-REL)(0 Trm)(NEGATE 0 Prop);
SELECT @N< IF (*-1 Abl BARRIER NOT-REL)(0 Abl)(NEGATE 0 Prop);
SELECT @N< IF (*-1 Ins BARRIER NOT-REL)(0 Ins)(NEGATE 0 Prop);
SELECT @N< IF (*-1 Aeq BARRIER NOT-REL)(0 Aeq)(NEGATE 0 Prop);
SELECT @N< IF (*-1 Via BARRIER NOT-REL)(0 Via)(NEGATE 0 Prop);
	## Una ilinniartitsisoq Trond Kirstip uia 40-nik ukioqarpoq.

REMOVE @N< IF (NEGATE -1 N OR @N<);
REMOVE @N< IF (-1 Prop)(0 Prop);

REMOVE (Abs @N<) IF (NEGATE *-2 (N Abs))(-1 (Rel @POSS))(0 (N 3Sg));
REMOVE (Lok @N<) IF (NEGATE *-2 (N Lok))(-1 (Rel @POSS))(0 (N 3Sg));
REMOVE (Trm @N<) IF (NEGATE *-2 (N Trm))(-1 (Rel @POSS))(0 (N 3Sg));
REMOVE (Abs @N<) IF (NEGATE *-2 (N Abl))(-1 (Rel @POSS))(0 (N 3Sg));
REMOVE (Ins @N<) IF (NEGATE *-2 (N Ins))(-1 (Rel @POSS))(0 (N 3Sg));
REMOVE (Aeq @N<) IF (NEGATE *-2 (N Aeq))(-1 (Rel @POSS))(0 (N 3Sg));
REMOVE (Via @N<) IF (NEGATE *-2 (N Via))(-1 (Rel @POSS))(0 (N 3Sg));
	## Karen Langgårdip illua.

REMOVE (Rel @N<) IF (NEGATE -1 (N Rel));
	## Atuagaq Trosterudip tiguaa.

REMOVE (Abs @N<) IF (NEGATE -1 Abs OR Rel);
REMOVE (Lok @N<) IF (NEGATE -1 Lok OR Rel);
REMOVE (Trm @N<) IF (NEGATE -1 Trm OR Rel);
REMOVE (Abs @N<) IF (NEGATE -1 Abs OR Rel);
REMOVE (Ins @N<) IF (NEGATE -1 Ins OR Rel);
REMOVE (Aeq @N<) IF (NEGATE -1 Aeq OR Rel);
REMOVE (Via @N<) IF (NEGATE -1 Via OR Rel);

	
# =========== #
#    Fald     # 
# =========== #
  CONSTRAINTS #
# =========== #

	# Hugseregel:
	# subj-abs IV
	# subj-rel obj-abs TV.



# Intransitive sætninger
# ====================== #

REMOVE @ALLOBJ> IF (0 Abs)(*1C INTRANSVERB) ;
	## Trond sinippoq.
	# Trond sover

REMOVE Rel IF (1 (V 3Sg))(NEGATE 1 TRANSVERB);
	## Nuummi najugaqarpoq.
	#  Han bor i Nuuk.

REMOVE (Abs MI) IF (0 Lok)(*1 LOKVERB);
	## Nuummi najugaqarpoq.
    ## Illumi aappaluttumi najugaqarpugut.

REMOVE (Rel 4Sg) IF (0 Lok)(*1 LOKVERB);
    ## Illumi aappaluttumi najugaqarpugut.



REMOVE Pl + @ALLSUBJ> IF (*1C VERBNOT3PLSUBJ BARRIER LU);

REMOVE (N Sg) + @ALLSUBJ> IF (*1C VFIN BARRIER LU LINK NOT 0 3SGSUBJVERB)(NEGATE 0 LU);

REMOVE (Rel @SUBJ>) IF (*1C INTRANSVERB BARRIER TRANSVERB);
	##       N-Rel N-Abs IV =      poss subj iv
    ## N-Rel N-Rel N-Abs TV = poss subj obj tv-s-o
    
# Transitive sætninger
# ==================== #

SELECT (Pron Abs 1Sg @SUBJ>) IF (*1 (V 1Sg));
	## Uanga takuara.


REMOVE @SUBJ> IF (0 N)(*1C 12SUBJVERB BARRIER V);
REMOVE @SUBJ> IF (0 (Pron 3Sg))(*1C 12SUBJVERB BARRIER V);

SELECT (Pl @OBJ>) IF (0 (Abs Pl))(*1 34PLOBJVERB);
SELECT (Pl @OBJ>) IF (0 (Abs Pl))(*-1 34PLOBJVERB);
	## Aviisit atuarpavut.

SELECT (N Abs Sg) + @ALLOBJ> IF (*1 34SGOBJVERB BARRIER Sg +  @ALLOBJ>)(NEGATE *1 ORATIOOBLVERB);
	#Pers aendring 0608

SELECT (Par @PRED) IF (*1 34SGOBJVERB)(*1 ORATIOOBLVERB); #Pers tilfoejelse 2 0608
 #SELECT (Abs Sg @OBJ>) IF (*1 34SGOBJVERB BARRIER (Sg @OBJ>));
SELECT (Abs Pl @OBJ>) IF (*1 34PLOBJVERB BARRIER (Pl @OBJ>));
	## Kaalip qimmini toquppaa.



SELECT (Abs Sg) IF (0C (@SUBJ>))(*1C 3SGSUBJVERB);


# NP-er
# -----

SELECT (Abs Sg) IF (-1 (Abs Sg))(0 LU);
	## Jaakup Juda qatanngutaalu.




REMOVE @SUBJ> IF (0 Rel)(1C (Abs @SUBJ>));
	## Nukap nulia Linimik ateqarpoq.
	# Nukan vaimon nimi on Line.
	# Gives Rel Poss

REMOVE (N @POSS) IF (NEGATE 1 POSSESSUM3 OR LU);
	## Nukap Piitap illua qalipappaa.
	#  ^^^^^

REMOVE (N @POSS) IF (1 LU)(NEGATE 2 POSSESSUM3);

REMOVE @SUBJ> IF (*-1C (@SUBJ>) BARRIER LU OR V)(NEGATE 0 LU);
	## Nukap Piitap illua qalipappaa.
	## Nukap Piitap ernerata illua qalipappaa.
    #        ^^^^^^

REMOVE @PROP> IF (*1 NOT-FIRST BARRIER Sur LINK NOT 0 Sur);
	## Per Morten Lynge
	## Per illu Lynge
	## Per Langgård   # 
	

REMOVE @POSS IF (1C POSSESSUM4);


# Verbal disambiguering
# =====================

	# Eit sikkert VFIN til venstre utelukker VFIN til høgre
REMOVE VFIN IF (*-1C VFIN)(NEGATE 0 LU); 

	# Unik VFIN
REMOVE VFIN IF (*1C VFIN LINK NOT 0 LU); 
	## Taamaattumik tamatta nuannaarpugut.


SELECT (@CL-<CIT) IF (*-1 ORATIOOBLVERB BARRIER V-NOT-Par) ;
SELECT (@CL-CIT>) IF (*1 ORATIOOBLVERB BARRIER V-NOT-Par) ;


SELECT Par IF (*-1 UNA BARRIER V-NOT-Par);
	## Trondimuna tigusimagaa.
	
REMOVE Par IF (-1C N)(NEGATE *-1 VEXCLAM);
 
SELECT Par IF (NEGATE *-1 VFIN)(NEGATE *1 VFIN OR ("?"));
	## Sunaaffa anereersut.


REMOVE @OBJ> IF (0 Abs)(*1 Par OR Cau)(*-1 ORATIOOBLVERB);
REMOVE @OBJ> IF (0 Abs)(*1 Par OR Cau)(*1 ORATIOOBLVERB); #Pers tilfoejelse01_00608
	## Nalunngilara Trond pikkorissoq.

REMOVE @OBJ> IF (0 Abs)(*1 (Par INTRANSVERB))(*-1 ORATIOOBLVERB);
	## Nalunngilara Trond pikkorissoq.

SELECT @SUBJ> IF (0 Rel)(*1 (Par TRANSVERB))(*-1 ORATIOOBLVERB);

REMOVE @OBJ> IF (0 Abs)(*1 (Par INTRANSVERB))(*-1 ORATIOOBLVERB);
	## Nalunngilara Trond pikkorissoq.

REMOVE Opt IF (*1C Ind) ;
REMOVE Int IF (*1C Ind) ;
REMOVE Imp IF (*1C Ind) ;


SELECT @SUBJ> IF (0 Rel)(*1 (Par TRANSVERB))(*-1 ORATIOOBLVERB);	
	
REMOVE Opt IF (*-1C Ind) ;
	
REMOVE Int IF (*-1C Ind) ;
	
REMOVE Imp IF (*-1C Ind) ;
	## Kaali anivoq.

REMOVE Opt IF (*1C Ind) ;
REMOVE Int IF (*1C Ind) ;
REMOVE Imp IF (*1C Ind) ;

REMOVE Opt IF (*-1C Ind) ;
	
REMOVE Int IF (*-1C Ind) ;
	
REMOVE Imp IF (*-1C Ind) ;
	## Kaali anivoq.

# Numerus
# -------

SELECT 3Pl IF (*-1 (LU @SUBJ>) LINK -1 (@SUBJ>))(0 (@PRED));
	## Nukap Linillu illortik qalipappaat.
	# Korrekt regel men feil eksempel.

SELECT 3SgO IF (*-1C (Sg @OBJ>)) ;
	## Nukap Linillu illortik qalipappaat.

SELECT 3PlO IF (*-1C (Pl @OBJ>)) ;



# ========================= #	
# Syntaktisk disambiguering #
# ========================= #
         CONSTRAINTS        #
# ========================= #

REMOVE @SUBJ> IF (NEGATE *-1 V)(NEGATE *1 V);
REMOVE @OBJ> IF (NEGATE *-1 V)(NEGATE *1 V);

REMOVE @HNOUN IF (0 (@OBJ>) OR (@SUBJ>));

	# a. try to find headnoun (try to find headlines, fragments)
	# b. if not, it has to be the VFIN



REMOVE @SUBJ> IF (NEGATE *-1 V)(NEGATE *1 V);
REMOVE @OBJ> IF (NEGATE *-1 V)(NEGATE *1 V);

REMOVE @HNOUN IF (0 (@OBJ>) OR (@SUBJ>));

REMOVE HUMAN + Abl IF (0 PLACE + Abl)(NEGATE 0 KKUT)(*1 ABLVERB);
	# NB plussnotasjon! XXX

REMOVE HUMAN + Trm IF (0 PLACE + Trm)(NEGATE 0 KKUT)(*1 TRMVERB);
REMOVE HUMAN + Lok IF (0 PLACE + Lok)(NEGATE 0 KKUT);

REMOVE PLACE + @SUBJ> IF (*1 HUMVERB BARRIER @SUBJ>);
	## Illumi sulivoq.

REMOVE FAM IF (NEGATE 0 3Sg)(0 OBLIQUE);
REMOVE @ADVL> IF (0 THING)(*1 PSEUDO-HTR BARRIER V);
REMOVE @OBJ> IF (0 INSTRUMENT)(*1 PSEUDO-HTR BARRIER V);

REMOVE (Prop Ins) IF (0 N + @OBJ>)(*1 PSEUDO-HTR BARRIER V);

# ========================= #
#     Final sektion         #
# ========================= #
         CONSTRAINTS        #
# ========================= #


SELECT COMMONAFFSTRING ;


 # Heuristiske regler:
 # I mangel av andre, tidligere regler, vælg de vanlige analyser
 # og smid væk de sjeldne.
 
 
# De måske ikke egnede

REMOVE @<SUBJ ;
REMOVE @<OBJ;
REMOVE @i-<SUBJ;
REMOVE @i-<OBJ;

