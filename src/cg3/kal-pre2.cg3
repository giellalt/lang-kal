#cg-pre-pipe: ~/langtech/kal/tools/shellscripts/kal-tokenise ~/langtech/kal/tools/tokenisers/tokeniser-disamb-gt-desc.pmhfst | /usr/local/bin/vislcg3 --grammar ~/langtech/kal/src/cg3/kal-pre1.cg3 #

#ADD-reglerne bedst placeret i selvstændig CG, fordi DIRTALE.*-tags ikke er synlige i første no-mappings kørsel (i modsætning til SUBSTITUTE, der kan ses straks, men til gengæld gentages et uskønt antal gange) samt fordi de nye tags ikke kan anvendes i constraints, der peger mod højre. Sidste forbehold gælder også for SUBSTITUTE.

#Der gøres (foreløbig?) ikke forskel på direkte tale modsat opremsninger o.lign. efter kolon. DIRTALE er altså lidt upræcis idet en sætning som " ... paasissutissaaleqilaarpungali: Aqqit nalinginnaarpianngitsut soorlu Agssagiaĸ suiaassuseqartinngilakka taamaammat suliassiissut manna ingerlassinnaanagu." vil få analysen " ... paasissutissaaleqilaarpungali:DIRTALESTART Aqqit nalinginnaarpianngitsut soorlu Agssagiaĸ suiaassuseqartinngilakka taamaammat suliassiissut manna ingerlassinnaanagu.DIRTALESLUT"
 

INCLUDE STATIC disambiguator.cg3 ;

#Tagging af hypotakser til brug i BARRIER. NB § kan ikke være præfix fordi §-tags slettes automatisk mellem CG-erne
ADD:ContHypotagme1 (%ContHypotagme) TARGET Abs + Sg IF (1 CONT + 3SgO);
ADD:ContHypotagme2 (%ContHypotagme) TARGET Abs + Pl  IF (1 CONT + 3PlO);
ADD:ContHypotagme3 (%ContHypotagme) TARGET CONT + 3SgO IF (-1 Abs + Sg);
ADD:ContHypotagme4 (%ContHypotagme) TARGET CONT + 3PlO IF (-1 Abs + Pl);
ADD:ContHypotagme5 (%ContHypotagme) TARGET Num + Abs IF (1 CONT + 3PlO); #"Fejlen" 18 sinnerlugit ukiullit
ADD:ContHypotagme6 (%ContHypotagme) TARGET CONT + 3PlO IF (-1 Num + Abs ); #"Fejlen" 18 sinnerlugit ukiullit
ADD:ContHypotagme7 (%ContHypotagme) TARGET Abs IF (1 Abs + LU LINK 1 CONT + 3PlO);  

ADD:NumPossessum1 (%NumPossessum) TARGET ("misse") + 3PlPoss IF (-1 Rel + Num) ;
ADD:NumPossessum2 (%NumPossessum) TARGET Rel + Num IF (1 ("misse") + 3PlPoss) ;
ADD:TidHypotagme01 (%TidHypotagme) TARGET TIME + Rel IF (1 TIME + OBLIQUE + POSSESSUM3); #aggustip qaammataani
ADD:TidHypotagme02 (%TidHypotagme) TARGET TIME + OBLIQUE + POSSESSUM3 IF (-1 TIME + Rel); #aggustip qaammataani
ADD:TidHypotagme03 %TidHypotagme TARGET (/"ulloq"\ N\ Rel\ Pl/l) IF (1 (/ila"\ N\ Lok\ Sg\ 3PlPoss/l));#ullut ilaanni
ADD:TidHypotagme03 %TidHypotagme TARGET (/ila"\ N\ Lok\ Sg\ 3PlPoss/l) IF (-1 (/"ulloq"\ N\ Rel\ Pl/l));#ullut ilaanni
ADD:TidHypotagme04 %TidHypotagme TARGET Rel IF (1 ("kinguneq") + POSSESSUM3 LINK *-1 BOS BARRIER (*) - Rel); #Tamatuma kingorna
ADD:TidHypotagme04 %TidHypotagme TARGET ("kinguneq") + POSSESSUM3 IF (-1 Rel LINK *-1 BOS BARRIER (*) - Rel); #Tamatuma kingorna
ADD:TidHypotagme04 %TidHypotagme TARGET Rel + CONJ-C IF (1 ("kinguneq") + POSSESSUM3 LINK *-1 KOMMA BARRIER (*) - Rel); #.., tamatumalu kingorna
ADD:TidHypotagme04 %TidHypotagme TARGET ("kinguneq") + POSSESSUM3 IF (-1 Rel + CONJ-C LINK *-1 KOMMA BARRIER (*) - Rel); #Tamatuma kingorna
ADD:TidHypotagme05 %TidHypotagme TARGET Rel + ("[0123456789]{4}"r) IF (1 Rel + ("[0123456789]{4}"r) + CONJ-C LINK 1 ("akunneq") + 3PlPoss + Lok); #1952-ip 1955-illu akornanni
ADD:TidHypotagme05 %TidHypotagme TARGET Rel + ("[0123456789]{4}"r) + CONJ-C IF (1 ("akunneq") + 3PlPoss + Lok); #1952-ip 1955-illu akornanni
ADD:TidHypotagme05 %TidHypotagme TARGET ("akunneq") + 3PlPoss + Lok IF (-2 Rel + ("[0123456789]{4}"r) LINK 1 ("[0123456789]{4}"r) + Rel + CONJ-C); #1952-ip 1955-illu akornanni

ADD:VerbalInderivering (%VerbalInderivering) TARGET (/Gram/IV.*\(NIRAR|SURE\)\ Der/vv.*\ Gram/TV\ V/l) ;
ADD:VerbalInderivering (%VerbalInderivering) TARGET (/Gram/IV.*\ SSA\ Der/vv\ NGA\ Der/vv\ TIP\ Der/vv\ Gram/TV\ V/l) ;
ADD:VerbalInderivering (%VerbalInderivering) TARGET (/Gram/IV\ NIRAR\ Der/vv\ Gram/TV\ Gram/Refl\ V/l) ;

#En interim-tag, der tillader enten en Abs eller en Rel i en BARRIER selv om pendanten endnu ikke er disambigueret. Skal slettes igen mellem morfologisk og syntaktisk disambiguering
ADD:AmbiguøsN (%AmbiguøsN) TARGET Abs IF (0 Rel);
ADD:AmbiguøsN (%AmbiguøsN) TARGET Rel IF (0 Abs);

#interim-tag, der accepterer ambiguøs Par i BARRIER
ADD:AmbiguøsPar (%AmbiguøsPar) TARGET Par IF (0 TUQ + N);

#tillad hypotakser med temporal indhold i BARRIER
ADD:%Othertime1 (%Othertime) TARGET ("ingerla") + &&POSS34_POSS& OR ("naa" NIQ) + &&POSS34_POSS& IF (*-1 TIMENOUN + Rel BARRIER (*) - ADVERBIEL - Der/vn);
ADD:%Othertime2 (%Othertime) TARGET ADVERBIEL + Rel OR Der/vn + Rel IF (*-1 TIMENOUN + &&POSS34_POSS& BARRIER (*) - ADVERBIEL - Der/vn)(*1 ("ingerla") + &&POSS34_POSS& BARRIER (*) - ADVERBIEL - Der/vn - NUM);
ADD:%Othertime3 (%Othertime) TARGET TIMENOUN + &&POSS34_POSS& IF (*1 ("ingerla") + &&POSS34_POSS& BARRIER (*) - ADVERBIEL - Der/vn - NUM - %AmbiguøsN); 
ADD:%Othertime3 (%Othertime) TARGET ("ingerla") + &&POSS34_POSS& IF (*-1 TIMENOUN + &&POSS34_POSS& BARRIER (*) - ADVERBIEL - Der/vn - NUM - %AmbiguøsN); 

#Forstår ikke følgende. Forsøgsvist ud og erstattet af ny regel
#ADD:%Othertime4 (%Othertime) TARGET TIMENOUN + Rel IF (1 OTHERTIME + Rel LINK 1 OTHERTIME + &&POSS34_POSS& BARRIER (*) - ADVERBIEL - Der/vn); 
ADD:%Othertime4 (%Othertime) TARGET TIMENOUN + Rel IF (1 OTHERTIME + Rel + &&POSS34_POSS& LINK *1 OTHERTIME + &&POSS34_POSS& BARRIER (*) - ADVERBIEL - Der/vn); 
ADD:%Othertime5 (%Othertime) TARGET ("kingorna") OR ("kinguneq") + POSSESSUM3 IF (-1 NIQ + Rel);

#Tillad possessor for OBLIQUE i type BARRIER (*) - OBLIQUE - %PossOblique
ADD:PossOblique (%PossOblique) TARGET Rel + &&POSS34_POSS& IF (*1 OBLIQUE + &&POSS34_POSS& BARRIER (*) - OBLIQUE); #Saqqummersitaq naleqqersuutinik ineriartornermi ANGUNIAKKAT sumut killinnerinut uuttuutaasinnaasunik imaqarpoq.

#Som hjælp til at adskille fysisk immikkoortoq fra immikkoortoq som meta etc.
ADD:place01 Sem/Geo BEFORE N TARGET (/"immikkoor"\ Gram/IV\ TUQ/l) OR ("ila") IF (-1 PLACE + Rel );

#Nyt afsnit påbegyndt 20210312 til "relativsætninger" med TAQ+POSSESSUM
ADD:TAQ_POSSESSUM01 (%TaqPossPoss) TARGET Rel - CONJ-C IF (1 TAQ + POSSESSUM3 + $$KASUS + $$NUMERUS)(-1 $$KASUS + $$NUMERUS)  #meeqqap ARNAP ikiugaata ornippaanga.
(NEGATE 1 (/"asser"\ Gram/TV\ TAQ/l))#sandsynligvis 'assigivaa'
(NEGATE 1 (/"pisiaq"\ GE\ Der/nv\ Gram/TV\ TAQ\ Der/vn\ PAK/l)) #som sandsynligvis er 'pisiarisarpa.*
;
ADD:TAQ_POSSESSUM02 (%TaqPossPoss) TARGET TAQ + POSSESSUM3 + $$KASUS + $$NUMERUS IF (-1 Rel - CONJ-C)(-2 $$KASUS + $$NUMERUS)  #meeqqap arnap IKIUGAATA ornippaanga.
(NEGATE 0 (/"pisiaq"\ GE\ Der/nv\ Gram/TV\ TAQ\ Der/vn\ PAK/l)) #som sandsynligvis er 'pisiarisarpa.*
(NEGATE 0 (/"asser"\ Gram/TV\ TAQ/l))#sandsynligvis 'assigivaa'
; 

#PL20210520 Hjælp til at isolere komplekse konstruktioner i BARRIER
ADD:%2smlNP1 (%2smlNP) TARGET Num IF (-1 $$KASUS + $$NUMERUS LINK 1 Num LINK 1 PROCENT + Abl OR Sem/Unit + Abl LINK 1 <fn:Be> + NIRU + $$KASUS + $$NUMERUS); #Naalakkersuisut Siulittaasuat Kim Kielsen aalajangerpoq imigassat aalakoornartortallit 2,25 PROCENTIMIT KIMITTUNERUSUT tamarmik Nuummi, Kapisilinni Qeqertarsuatsiaanilu tuniniarneqartarneri sassaalliutigineqartarnerilu inerteqqutigineqarallassasut.
ADD:%2smlNP2 (%2smlNP) TARGET PROCENT + Abl OR Sem/Unit + Abl IF (-2 $$KASUS + $$NUMERUS LINK 1 Num LINK 1  _TARGET_ LINK 1 <fn:Be> + NIRU + $$KASUS + $$NUMERUS); #Naalakkersuisut Siulittaasuat Kim Kielsen aalajangerpoq imigassat aalakoornartortallit 2,25 PROCENTIMIT KIMITTUNERUSUT tamarmik Nuummi, Kapisilinni Qeqertarsuatsiaanilu tuniniarneqartarneri sassaalliutigineqartarnerilu inerteqqutigineqarallassasut.
ADD:%2smlNP3 (%2smlNP) TARGET <fn:Be> + NIRU + $$KASUS + $$NUMERUS IF (-3 $$KASUS + $$NUMERUS LINK 1 Num LINK 1 PROCENT + Abl OR Sem/Unit + Abl LINK 1 _TARGET_); #Naalakkersuisut Siulittaasuat Kim Kielsen aalajangerpoq imigassat aalakoornartortallit 2,25 PROCENTIMIT KIMITTUNERUSUT tamarmik Nuummi, Kapisilinni Qeqertarsuatsiaanilu tuniniarneqartarneri sassaalliutigineqartarnerilu inerteqqutigineqarallassasut.


#Parataktiske NP af typen CONT-LU og N
ADD:PartaktiskNP (%ParataktiskNP) TARGET CONT + QAR + 4Pl + CONJ-C IF (1 LIK + N) ;

#Farlige interimregler importeret fra "Livs GCG" mens vi venter på at regressionen gøres kontekstsensitiv
ADD:DIRSTART_Int1 DIRTALESTART TARGET ("-") + PUNCT OR (""") + PUNCT OR ("”") + (PUNCT RIGHT) OR ("„") + (PUNCT LEFT) IF (-1 BOS) ;
ADD:DIRSTART_Int2 DIRTALESTART TARGET (":") IF (NEGATE 0 DIRTALESTART OR DIRTALESLUT) 
(NEGATE -2 BOS LINK 1 ("[A-Z]"r))#Ikke punktopstillinger som A: etc.
; #not safe marking

ADD:DIRSLUT_Int01 DIRTALESLUT TARGET CLB IF (0 ("?") OR ("!")) (*-1< DIRTALESTART OR DIRTALEMIDDLE BARRIER DIRTALESLUT)(NEGATE 0 DIRTALESLUT) (NEGATE 0 DIRTALESTART)(NEGATE *1> DIRTALESLUT OR PUNCT BARRIER DIRTALESTART OR DIRTALEMIDDLE) ; #not safe marking


ADD:DIRSTART01 DIRTALESTART TARGET ("-") + PUNCT OR (""") + PUNCT IF (-*1W (":") + EOS BARRIER EOS);
ADD:DIRSTART02 DIRTALESTART TARGET ("-") + PUNCT IF (-*1W (".") + EOS );#BARRIER EOS);
ADD:DIRSTART03 DIRTALESTART TARGET (""") + PUNCT IF (-*1 VFIN + INQUIT_VERB BARRIER VFIN);


ADD:DIRTALESLUT1 DIRTALESLUT TARGET (""") + PUNCT IF (-*1W (".") + EOS OR ("!") + EOS OR ("?") + EOS BARRIER EOS);

ADD:DIRSLUT01 DIRTALESLUT TARGET ("?") + CLB OR ("!") + CLB OR (".") + CLB IF (*-1W DIRTALESTART BARRIER CLB - KOMMA)
(NEGATE *1W (""") + PUNCT BARRIER EOS)#Ikke slut hvis anført tale i citationstegn
(NEGATE *-1 KOMMA LINK 1 Prop OR N LINK *1 <fn:Talk> + Ind + 3Sg BARRIER VERB-NOTCONT)#men ikke kommaseparerede inquit jf. DIRSLUT02
(NEGATE *-1 ("?") LINK 1 Prop OR N LINK *1 <fn:Talk> + Ind + 3Sg BARRIER VERB-NOTCONT)#men ikke kommaseparerede inquit jf. DIRSLUT02
 ;
ADD:DIRSLUT02 DIRTALESLUT TARGET KOMMA + CLB IF (*-1 DIRTALESTART)(1 Prop + Abs LINK *1 ("oqar") + Ind BARRIER VERB-NOTCONT)
#(NEGATE # 
;#til inquit uden citationstegn slut og uden !, ? eller .


ADD:DIRSLUT03 DIRTALESLUT TARGET KOMMA + CLB IF (*-1 DIRTALESTART)(*1 Abs + Sg LINK *1 <fn:Talk> + 3Sg);

ADD:DIRSLUT00 DIRTALESLUT TARGET ("?") + CLB OR ("!") + CLB OR (".") + CLB IF (*-1 DIRTALESTART OR DIRTALEMIDDLE BARRIER DIRTALESLUT);


SUBSTITUTE DIRTALESLUT DIRTALEMIDDLE TARGET DIRTALESLUT IF (NEGATE *1W ("¶") + CLB BARRIER CLB)(NEGATE *1W <fn:Talk> + 3Sg BARRIER CLB - KOMMA OR DIRTALESLUT); #Ved interpunktion inde i DIRTALE: Trin 1: Gør den mediale DIRTALESLUT til en DIRTALESTART. Trin 2: Fjern de overflødige DIRTALESTART
#SUBSTITUTE DIRTALESTART (*) TARGET DIRTALESTART IF (*-1 DIRTALESTART)(NEGATE 0 DIRTALESTART LINK -*1 DIRTALESLUT) ;

#dummy-regel for at teste DIRTALE-grænser
#MAP @X TARGET ("taama") IF (-1W DIRTALESLUT) ;

#en test med tagging af særlige konstruktioner, der skal bruges tidligt i næste CG. Virker måske og måske ikke
#MAP:M1130 (@i-ADVL>) TARGET CONT IF (*1 NIQ - ("inuk" U NIQ) - Gram/Int - Gram/Comp OR (/.*neq"\ N/l) - ("seqineq") - ("aqqusineq") - ("inuk" U NIQ) - Gram/Int - Gram/Comp BARRIER (*) - Adv - ADVERBIEL LINK NOT 1 KOMMA) #Uangalu anorip karrit aqqusaarlugit siorsunnera nuannaringaalissagaluarpara... # Mapping af ledsagemåde til inderiveret verbum
#(NEGATE *-1 BOS LINK *1 _TARGET_ + QQU BARRIER (*) - Abs - OBLIQUE - Adv LINK 1 TIME)#Panini namminermisut inuunermik taamaattumik ATUGAQAQQUNAGU silattunnginnerani toqoqatigalugu inuunera kipiniaraluarpaa ...! 
#;
