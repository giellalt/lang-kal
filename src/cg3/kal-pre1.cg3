# -*- cg-pre-pipe: zsh -l -c '~/langtech/kal/tools/shellscripts/kal-analyse --fst' -*-

# Nutserut/Gloss kal2eng pre-pipe: zsh -l -c '~/langtech/gloss/kal2eng/kal2eng.pl --fst'

INCLUDE STATIC disambiguator.cg3 ;

# kal-pre1.cg3 er nødvendig, fordi REMCOHORT fjerner en delimiter, hvilket betyder, at der efterlades et tomt vindue. Bundlinien er, at man ikke kan fjerne delimiters og derefter bruge den i samme CG



# ============ #
# Regelsektion #
# ============ #

BEFORE-SECTIONS


REMCOHORT:paragraftegn ("¶") + CLB IF (-*1W (":") + EOS OR (".") + EOS BARRIER EOS);
#BARRIER EOS for at sikre at det kun er den foregående sætning og ikke noget langt til venstre

MergeCohorts ("<$1,->"v "$1,-"v Num Abs Sg) ("<(.+)>"r Num Abs Sg) WITH (1 ("<,>")) (2 ("<->")) ;


SPLITCOHORT ("<$1>"v "$1"v Num Abs Sg "<$2>"v "$2"v CLB) ("<(\\p{N}+)([.])>"r Gram/Ord) (-1 BOS) (1 ("<\\p{Lu}.*"r)) (NOT 1 MAANED) ;

#Ad hoc disambiguering, fordi kalaallisooq=SSAQ + Via + Sg + 3SgPoss blokerer for hybridsplittet
REMOVE:kalaallisooq (/"kalaallisooq"\ SSAQ\ Der/nn\ N\ Via\ Sg\ 3SgPoss/l) IF (0 (/"kalaaleq"\ iN\ iAeq\ iPl\ Gram/Hyb\ UR\ Gram/IV\ SSA/l));

SPLITCOHORT ("<$1>"v "$1"v Num Abs Sg "<$2>"v "$2"v CLB) ("<(\\p{N}{4})([.])>"r Gram/Ord) (0 EOS) ;
SPLITCOHORT ("<$1>"v "$1"v Num Abs Sg "<$2>"v "$2"v CLB) ("<(\\p{N}{4})([.])>"r Gram/Ord) (1 ("¶") + EOS) ;

#splitting compounded by error
SPLITCOHORT ("<$1>"v "$1"v "<$2>"v "$2"v) TARGET ("<(\\p{Lu}?\\p{Ll}+)\\u003A(\\p{Lu}\\p{Ll}+)>"r ?) ; #Jude 20240202
SPLITCOHORT ("<$1>"v "$1"v "<$2>"v "$2"v) TARGET ("<(\\p{Lu}?\\p{Ll}+)(\\p{Lu}\\p{Ll}+)>"r ?) + HeurGLfinal ; #Jude 20240202
SPLITCOHORT ("<$1>"v "$1"v "<$2>"v "$2"v) TARGET ("<(\\d\\u002D\\p{Ll}+)(\\p{Lu}\\p{Ll}+)>"r ?) + HeurGLfinal ; #Jude 20240202
SPLITCOHORT ("<$1>"v "$1"v "<$2>"v "$2"v) TARGET ("<(\\p{Lu}?\\p{Ll}+)(\\p{Lu}+)>"r ?) + HeurGLfinal ; #Jude 20240202

#Heuristics
#Heur/Prop Heur/Quote
Substitute (".*"r) ("$1"v <quote-1>) TARGET ("<\(\\p{Lu}.*\)>"r) (-1 ("<(»|„)>"r) LINK 1* ("<(«|“)>"r) BARRIER CLB LINK 1* ("<(-|mi|ni|imi|ini|imiit|miit|mit|imut|mut|gooq|rooq)>"r) OR CLB) 
(NEGATE 0 ("<.*>"r) + WFORM LINK 0 ("$1"v) LINK NEGATE 1* ("<(-|mi|ni|imi|ini|imiit|miit|mit|imut|mut|gooq|rooq)>"r)) 
(NEGATE 0 TUQ) ;
Substitute (".*"r) ("$1"v <quote-2>) TARGET ("<\(.*\)>"r) (-1 (<quote-1>)) ; 
Substitute (".*"r) ("$1"v <quote-3>) TARGET ("<(«|“|i?mi|ni|i?mut|\\p{Ll}+)>"r) (-1 (<quote-2>)) ; 
Substitute (".*"r) ("$1"v <quote-4>) TARGET ("<(«|“|i?mi|ni|i?mut)>"r) (-1 (<quote-3>)) ;
Substitute (".*"r) ("$1"v <quote-5>) TARGET ("<(-|«|“|i?mi|ni|i?mut|i?mi?it)>"r) (-1 (<quote-4>)) ;  
Substitute (".*"r) ("$1"v <quote-6>) TARGET ("<(-|«|“|i?mi|ni|i?mut|i?mi?it)>"r) (-1 (<quote-5>)) ;  
Substitute:quoteAbsSg (".*"r PUNCT LEFT) ("<„$1$2$3>"v "$1"v Prop Abs Sg GUUQ Heur/Prop Heur/Quote) TARGET ("<„>") (1 ("<\(\\p{Lu}.*\)>"r) LINK 1 ("<\(“\)>"r) LINK 1 ("<\([g|r]ooq\)>"r)) ; # „Pharao“gooq 
Substitute:quoteAbsSg (".*"r PUNCT RIGHT) ("<»$1 $2$3>"v "$1 $2"v Prop Abs Sg Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r <quote-1>) LINK 1 ("<\(.*\)>"r) LINK 1 ("<\(«\)>"r) LINK 1* 3SGSUBJVERB LINK NEGATE -1* _TARGET_ LINK -1* Abs + Sem/Hum BARRIER Abs - OTHERTIME - SEASON - Sem/dur) ; # »Oqaluttualiaativut taalliaativullu«
Substitute:quoteTrmSg (".*"r PUNCT RIGHT) ("<»$1 $2 $3$4$5>"v "$1 $2 $3"v Prop Abl Sg Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r) LINK 1 ("<\(.*\)>"r) LINK 1 ("<\(.*\)>"r) LINK 1 ("<\(«\)>"r) LINK 1 ("<->") LINK 1 ("<\(i?mi?it\)>"r)) ; # »Fänrik Ståls sägner«-imiit
Substitute:quoteLokSg (".*"r PUNCT RIGHT) ("<»$1 $2$3$4>"v "$1 $2"v Prop Lok Sg Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r) LINK 1 ("<\(.*\)>"r) LINK 1 ("<\(«\)>"r) LINK 1 ("<\(i?mi\)>"r)) ; # »Atuarfiit ilagiillu«mi
Substitute:quoteLokPl_Sg (".*"r PUNCT RIGHT) ("<»$1 $2$3$4>"v "$1 $2"v Prop Lok Pl Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r) LINK 1 ("<\(.*\)>"r) LINK 1 ("<\(«\)>"r) LINK 1 ("<\(ni\)>"r)) ;  
COPY (Prop Lok Sg Heur/Prop Heur/Quote) EXCEPT (Prop Lok Pl Heur/Prop Heur/Quote) TARGET Lok + Pl + Heur/Prop ; # »Ukiut 300-nngornerat«ni
Substitute:quoteLokSg (".*"r PUNCT RIGHT) ("<»$1$2$3>"v "$1"v Prop Lok Sg Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r) LINK 1 ("<\(«\)>"r) LINK 1 ("<\(i?mi\)>"r)) ; # »Atuarfiit«mi
Substitute:quoteTrmSg (".*"r PUNCT RIGHT) ("<»$1$2$3>"v "$1"v Prop Trm Sg Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r <quote-1>) LINK 1 ("<\(«\)>"r) LINK 1 ("<\(i?mut\)>"r)) ; # »Kamikken«imut
Substitute:quoteTrmSg (".*"r PUNCT RIGHT) ("<»$1 $2$3$4>"v "$1 $2"v Prop Trm Sg Heur/Prop Heur/Quote) TARGET ("<»>") (1 ("<\(\\p{Lu}.*\)>"r) LINK 1 ("<\(.*\)>"r) LINK 1 ("<\(«\)>"r) LINK 1 ("<\(i?mut\)>"r)) ; # »Kamikken«imut
Remcohort (<quote-1>) ;
Remcohort (<quote-2>) ;
Remcohort (<quote-3>) ;
Remcohort (<quote-4>) ;
Remcohort (<quote-5>) ;
Remcohort (<quote-6>) ;

#Heur abbr
Substitute ("<.*>" ?) ("$1"v N Gram/Abbr Abs Sg Heur/Abbr) TARGET ("\(\\p{Ll}\\p{Ll}\)"r ?) - For2Let ; #Jude 20240208
Substitute ("<.*>" ?) ("$1"v N Gram/Abbr Abs Sg Heur/Abbr) TARGET ("\(\\p{Lu}\\p{Ll}\)"r ?) - For2Let - HeurExcl ; #Jude 20240208
Substitute ("<.*>" ?) ("$1"v N Gram/Abbr Abs Sg Heur/Abbr) TARGET ("\(\\p{Ll}\\p{Lu}\)"r ?) - For2Let - HeurExcl ; #Jude 20240208
#Heur/Excl
Substitute:HeurExcl ("<.*>"r ?) ("$1"v Heur/Excl) TARGET ("<\(.*\)>"r ?) + HeurExcl - HeurGLfinal - HeurGLErr - HeurConsIllGL - RomNum ; #Jude 20240206

# Specific heuristic analysis first
#Heur University of
SUBSTITUTE (?) (<university-of-> ?) TARGET ("\\p{Lu}.*"r ?) (-1 ("of") LINK -1 ("University")) ; #Jude 20240129
SUBSTITUTE (Sem/Geo Prop Abs Sg) (Sem/Geo Prop Abs Sg <university-of->) TARGET ("\\p{Lu}.*"r) (-1 ("of") LINK -1 ("University")) ; #Jude 20240129
SUBSTITUTE (Sem/Geo Prop Abs Sg) (Sem/Geo Prop Abs Sg <university-of-X->) TARGET ("\\p{Lu}.*"r) (-1 (<university-of->) LINK -1 ("of") LINK -1 ("University")) 
(NEGATE 0 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?[mi|mut]>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Rel_1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Rel Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?p\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_Rel2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Rel Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?p\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Lok1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Lok Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mi\)>"r)) ; #Jude 20240129 University of Manitobami
MergeCohorts:HeurProp_Lok2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Lok Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mi\)>"r)) ; #Jude 20240129 University of British Columbiami
MergeCohorts:HeurProp_Lok3 ("<$1=$2=$3=$4=$5$6>"v "$1=$2=$3=$4=$5"v Prop Lok Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r Sem/Geo Abs) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mi\)>"r)) ; #Jude 20240129 University of Northern British Columbiami

MergeCohorts:HeurProp_Trm1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mut\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_Trm2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r Sem/Geo Abs) - Gram/Hyb LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mut\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Abl1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mit\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_Abl2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r Sem/Geo Abs) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mit\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Abs ("<$1=$2=$3>"v "$1=$2=$3"v Prop Abs Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r) - HeurOblCase) ; #Jude 20240129

MergeCohorts:HeurProp_meersoqAbs ("<$1=$2=$3$4>"v "$1=$2=$3"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Sg Heur/N) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?meersoq\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_meersoqRel ("<$1=$2=$3$4>"v "$1=$2=$3"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Sg Heur/N) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?meersup\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_meersoqAbsPl ("<$1=$2=$3$4>"v "$1=$2=$3"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Pl Heur/N %AmbiguøsN) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?meersut\)>"r)) ; #Jude 20240129
COPY:HeurProp_meersoqRelPl (N Rel) EXCEPT (N Abs) ("<University=of=.*>"r Heur/N %AmbiguøsN) ; #Jude 20240129

REMCOHORT:HeurOf ("of" ?) (-1 ("<University=of=.*>"r) + Heur/Prop OR ("<University=of=.*>"r) + Heur/N) ; #Jude 20240129
REMCOHORT:HeurProp (<university-of->) (-1 ("<University=of=.*>"r Heur/Prop)) ; #Jude 20240129
REMCOHORT:HeurProp (<university-of-X->) (-1 ("<University=of=.*>"r Heur/Prop)) ; #Jude 20240129

#Heur LOK specific and irregular
Substitute:HeurLokNunavut ("\\p{Lu}.*"r ?) ("Nunavut" Prop Lok Sg Heur/Prop) TARGET ("<Nunavumi>" ?) ; #Jude 20240129
Substitute:HeurLokNunavut ("\\p{Lu}.*"r ?) ("Nunavut" Prop Lok Sg Heur/Prop) TARGET ("<Nunavummi>" ?) ; #Jude 20240129


# Heur/Prop all cases, NOT mainly intended for input adjusted for greenlandic pronunciation: Buuarsi/Boas, Gustaat/Gustav

#Heur Abl
Substitute:HeurAblSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Abl Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?miillu>"r ?) ; #Jude 20240201
Substitute:HeurAblSg ("\\p{Lu}.*"r ?) ("$1"v Prop Abl Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)?)-?i?mi?it>"r ?) ; #Jude 20240129
Substitute:HeurAbl ("\\p{Lu}.*"r ?) ("$1"v Prop Abl Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nit>"r ?) ; #Jude 20240129
COPY:HeurAblAbs ("$1n"v Abs Pl Heur/Prop) EXCEPT ("\\p{Lu}.*"r Abl Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nit>"r) + Abl + Pl + Heur/Prop
(NEGATE 0 ("<.*ninit>"r)) ; #Jude 20240129

#Heur iAbl
Substitute:HeuriAblMeersoqAbsSg ("\\p{Lu}.*"r ?) ("$1"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Sg HeurDer Heur/N) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?meersoq>"r ?) ; #Jude 20240130
Substitute:HeuriAblMeersoqAbsPl ("\\p{Lu}.*"r ?) ("$1"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Pl HeurDer Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?meersut>"r ?) ; #Jude 20240130
Substitute:HeuriAblMeersoqInsSg ("\\p{Lu}.*"r ?) ("$1"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Ins Sg HeurDer Heur/N) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?meersumik>"r ?) ; #Jude 20240201
COPY:HeuriAblMeersoqRelSg (N Rel Pl HeurDer Heur/Prop) EXCEPT (N Abs Pl HeurDer Heur/Prop) TARGET iAbl + HeurDer + Heur/Prop ;
Substitute:HeuriAblMeersoqRelSg ("\\p{Lu}.*"r ?) ("$1"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Rel Sg HeurDer Heur/N) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)\\u002D?i?meersup>"r ?) ; #Jude 20240130

#Heur Aeq
Substitute:HeurAeq ("\\p{Lu}.*"r ?) ("$1"v Prop Aeq Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?[st]ut>"r ?) - HeurGLErr - HeurConsIllGL ; #Jude 20240129
COPY:HeurAeq (Aeq Pl Heur/Prop) EXCEPT (Aeq Sg Heur/Prop) TARGET Aeq + Heur/Prop ; #Jude 20240129

#Heur Ins
Substitute:HeurKkutIns ("\\p{Lu}.*"r ?) ("$1"v KKUT Der/nn Prop Ins Pl Heur/Prop Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)kkunnik>"r ?) ; #Jude 20240130
Substitute:HeurInsSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Ins Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?millu>"r ?) ; #Jude 20240130
Substitute:HeurInsSg ("\\p{Lu}.*"r ?) ("$1"v Prop Ins Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mik>"r ?) - HeurGLfinal - HeurGLErr ; #Jude 20240129
Substitute:HeurInsPl ("\\p{Lu}.*"r ?) ("$1"v Prop Ins Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nik>"r ?) ; #Jude 20240129

#Heur  iLok
Substitute:HeuriLok ("\\p{Lu}.*"r ?) ("$1"v Sem/Geo iProp iLok iSg Gram/Hyb IP Gram/IV SIMA Der/vv V Cont 4Sg LU Heur/iLok) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?miissimallunilu>"r ?) ; #Jude 20240129

#Heur Lok
Substitute:HeurLokSg ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mi>"r ?) - ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mmi>"r ?) - HeurGLErr ; #Jude 20240129
COPY:HeurLokAbsMi (Prop Abs Sg MI Heur/Prop) EXCEPT (Prop Lok Sg Heur/Prop) TARGET Prop + Lok + Sg + Heur/Prop - Heur/Quote ; #Jude 20240130
COPY:HeurLokRelMi (Prop Pel Sg MI Heur/Prop) EXCEPT (Prop Lok Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mmi>"r) + Prop + Lok + Sg + Heur/Prop ; #Jude 20240130
Substitute:HeurKkutLok ("\\p{Lu}.*"r ?) ("$1"v KKUT Der/nn Prop Lok Pl Heur/Prop Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)?)i?kkunni>"r ?) ; #Jude 20240130
Substitute:HeurLokSgLi ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg LI Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mili>"r ?) ; #Jude 20240130
Substitute:HeurLokSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?milu>"r ?) ; #Jude 20240129
Substitute:HeurLokLuunniit ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg LUUNNIIT Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?miluunniit>"r ?) ; #Jude 20240129
Substitute:HeurLokPl ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?ni>"r ?) - HeurConsIllGL - HeurGLErr ; #Jude 20240129
COPY:HeurLokAbs ("$1n"v Prop Abs Sg Heur/Prop) EXCEPT ("$1"v Prop Lok Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?ni>"r Prop Lok Pl Heur/Prop) - ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?kkun)ni>"r Prop Lok Pl Heur/Prop) ; #Jude 20240130

#Heur Rel Sg
Substitute:HeurRelSgGuuq ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg GUUQ Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?nngooq>"r ?) ; #Jude 20240201
Substitute:HeurRelSgLi ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg LI Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?lli>"r ?) - HeurScnErrF - HeurGLfinal ; #Jude 20240129
Substitute:HeurRelSgMiuLu ("\\p{Lu}.*"r ?) ("$1"v iProp MIU Der/nn N Rel Sg LU HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?miullu>"r ?) ; #Shemeshimiullu
Substitute:HeurRelSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg LU Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?llu>"r ?) - ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?mullu>"r ?) - ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?miillu>"r ?) ; #Jude 20240129
Substitute:HeurRelSgMi ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg MI Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?mmi>"r ?) ; #Jude 20240130
Substitute:HeurRelSgMiu ("\\p{Lu}.*"r ?) ("$1"v iProp MIU Der/nn N Rel Sg HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?miup>"r ?) ; #Shemeshimiup
Substitute:HeurRelSg ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?p>"r ?) - HeurForFinalP - HeurGLfinal - InitErr ; #Jude 20240129
Substitute:HeurRelSg ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u0027?i?p>"r ?) - HeurForFinalP - HeurGLfinal ; #Jude 20240129

#Heur Trm
Substitute:HeurKkutTrm ("\\p{Lu}.*"r ?) ("$1"v KKUT Der/nn Prop Trm Pl Heur/Prop Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?k?kunnut>"r ?) ; #Jude 20240130
Substitute:HeurTrmLu ("\\p{Lu}.*"r ?) ("$1"v Prop Trm Sg LU Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?mullu>"r ?) - HeurGLfinal ; #Jude 20240129
Substitute:HeurTrmSg ("\\p{Lu}.*"r ?) ("$1"v Prop Trm Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?mut>"r ?) - HeurGLfinal - HeurScnErrF - HeurGLErr ; #Jude 20240129
Substitute:HeurTrmPl ("\\p{Lu}.*"r ?) ("$1"v Prop Trm Pl Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?nut>"r ?) - HeurGLfinal - HeurConsIllGL - HeurGLErr ; #Jude 20240129

#Heur Via
Substitute:HeurVia ("\\p{Lu}.*"r ?) ("$1"v Prop Via Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?kkut>"r ?) ; #Jude 20240129
COPY:HeurViaKkut (KKUT Der/nn Prop Abs Pl HeurDer Heur/Prop) EXCEPT (Prop Via Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?kkut>"r) + Via + Sg + Heur/Prop ;
COPY:HeurViaKkut (KKUT Der/nn Prop Rel Pl HeurDer Heur/Prop) EXCEPT (Prop Via Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?kkut>"r) + Via + Sg + Heur/Prop ;
Substitute:HeurVia ("\\p{Lu}.*"r ?) ("$1"v Prop Via Pl Heur/Prop) TARGET ("(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?tigut>"r ?) ; #Jude 20240129

#Heur AbsRel Pl
Substitute:HeurAbsRelKkut ("\\p{Lu}.*"r ?) ("$1"v iProp KKUT Der/nn N Abs Pl HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?kkut>"r ?) ; #Jude 20240130
Substitute:HeurAbsRelKkutLu ("\\p{Lu}.*"r ?) ("$1"v iProp KKUT Der/nn N Abs Pl LU HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?kkullu>"r ?) ; #Jude 20240130
Substitute:HeurAbsRelKkutMiu ("\\p{Lu}.*"r ?) ("$1"v iProp KKUT Der/nn MIU Der/nn N Abs Pl HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?kkormiut>"r ?) ; #Jude 20240130
Substitute:HeurAbsRelMiu ("\\p{Lu}.*"r ?) ("$1"v iProp MIU Der/nn N Abs Pl HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?miut>"r ?) ; #Jude 20240129
Substitute:HeurAbsRTarToq ("\\p{Lu}.*"r ?) ("$1"v iProp R Der/nv Gram/IV TAR Der/vv Gram/IV TUQ Der/vn N Abs Pl HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?rtartut>"r ?) ; #Jude 20240201
Substitute:HeurAbsRelPl ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Pl Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?t>"r ?) - HeurVerb - HeurGLfinal - HeurForFinalT - HeurScnErrF - HeurGLErr - HeurConsIllGL - HeurOblCase
(NEGATE 0 ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?aasiit>"r ?)) 
(NEGATE 0 ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?k?kut>"r ?)) ; #Jude 20240129
COPY:HeurAbsRel (Rel Pl Heur/Prop) EXCEPT (Abs Pl Heur/Prop) TARGET Abs + Pl + Heur/Prop - HeurDer; #Jude 20240129
COPY:HeurAbsRel ("$1t"v Abs Sg Heur/Prop) EXCEPT ("$1"v Abs Pl Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?t>"r) + Abs + Pl + Heur/Prop - HeurDer - HeurForFinalN ; #Jude 20240130
COPY:HeurAbsRel (Rel Pl HeurDer Heur/N) EXCEPT (Abs Pl HeurDer Heur/N) TARGET Abs + Pl + Heur/N ; #Jude 20240129

#Heur Abs Sg
Substitute:HeurAbsAasiit ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg AASIIT Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?aasiit>"r ?) ; #Jude 20240130
Substitute:HeurAbsLi ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg LI Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?li>"r ?) - HeurOblCase - HeurRel - HeurScnErrF - HeurGLfinal ; #Jude 20240129
COPY:HeurAbs-Li ("$1l"v Prop Abs Sg Heur/Prop) EXCEPT ("$1"v Prop Abs Sg LI Heur/Prop) TARGET ("(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?li>"r ?) ; #Jude 20240129
Substitute:HeurAbsLu ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg LU Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?i?lu>"r ?) - HeurOblCase - HeurRel - HeurGLfinal ; #Jude 20240129
Substitute:HeurAbsTaaq ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg TTAAQ Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?issaaq>"r ?) - HeurOblCase - HeurRel ; #Jude 20240130
Substitute:HeurAbsTaaq ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg TTAAQ Heur/Prop) TARGET ("(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?ttaaq>"r ?) - HeurOblCase - HeurRel ; #Jude 20240130
Substitute:HeurAbsRTarToq ("\\p{Lu}.*"r ?) ("$1"v iProp R Der/nv Gram/IV TAR Der/vv Gram/IV TUQ Der/vn N Abs Sg HeurDer Heur/N) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?)\\u002D?rtartoq>"r ?) ; #Jude 20240201
Substitute:HeurAbsSg ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?)?\\p{Lu}?)\\u002D?i?>"r ?) - HeurOblCase - HeurRel - HeurVerb - HeurGLfinal - HeurForFinalI - HeurScnErrF - HeurExcl - Heur/Foreign ; #Jude 20240129
#Substitute:HeurAbsSg ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+)\\u002D?i?>"r ?) - HeurOblCase - HeurVerb - HeurForFinalI - HeurScnErrF ; #Jude 20240129
Substitute:HeurAbsSgFor_i ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?(\\u002D\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)?i)>"r ?) + HeurForFinalI - HeurOblCase - HeurRel - HeurVerb - HeurGLfinal ; #Jude 20240129

#Heur/Num
Substitute:HeurCoordinate ("<[0-9].*>"r ?) ("$1"v Num Abs Pl Heur/Num) TARGET ("<([NV]?\\d{2}(\\u00B0|\\u00BA|\\u02DA|\\u006F)\\d{2}(\\u0027|\\u2032)?([NWSE]|\\d{2})?(\\u0022|\\u2033)?([NWSE]|\\d)?)>"r ?) ; #Jude 20240131 taken common errors into consideration: Degree Sign -> Masculine Ordinal Indicator or Ring Above, Prime -> Apostrophe, Double Prime -> Quotation Mark

Substitute:HeurDegrees ("<[0-9].*>"r ?) ("$1"v Num Abs Pl Heur/Num) TARGET ("<(\\d+(\\u00B0|\\u00BA|\\u02DA)[CF]?)>"r) ; #Jude 20240131 taken common error into consideration Degree Sign -> Masculine Ordinal Indicator or Ring Above

Substitute:HeurDimensions ("<[0-9].*>"r ?) ("$1"v Num Abs Pl Heur/Num) TARGET ("<(\\d+\\u0078\\d+)>"r) ;

Substitute:HeurNumRel ("[0-9].*"r ?) ("$1"v Num Rel Pl Heur/Num) TARGET ("<(\\d+)(\\u0027|\\u002D)i?p>"r ?) ;
Substitute:HeurNumRelPl_t ("[0-9].*"r ?) ("$1"v Num Rel Pl Heur/Num) TARGET ("<(\\d+)(\\u0027|\\u002D)?i?t>"r ?) ;
COPY:HeurNumRel_t (Num Abs Pl Heur/Num) EXCEPT (Num Rel Pl Heur/Num) TARGET ("<\\d+(\\u0027|\\u002D)?i?t>"r) + Heur/Num ; 
Substitute:HeurNumAbl ("[0-9].*"r ?) ("$1"v Num Abl Pl Heur/Num) TARGET ("<(\\d+)(\\u0027|\\u002D)?i?mît>"r ?) ;
Substitute:HeurNum ("[0-9].*"r ?) ("$1"v Num Abs Pl Heur/Num) TARGET ("<(\\d+.*)>"r ?) ;

#Heur ACR
#Heur ACR Abl
Substitute:HeurACRAblSg ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Abl Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?mit>"r ?) ; #Jude 20240131
COPY:HeurACRAblSg ("$1"v Prop Abl Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Abl Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?mit>"r) + Heur/ACR ; #Jude 20240131

#Heur ACR Lok
Substitute:HeurACRLokSg ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Lok Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+)?)(\\u002D|\\u0027)?i?mi>"r ?) ; #Jude 20240131
COPY:HeurACRLokSg ("$1"v Prop Lok Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Lok Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?mi>"r) + Heur/ACR ; #Jude 20240131

#Heur ACR Ins
Substitute:HeurACRInsSg4SgPoss ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Ins 4SgPoss Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+)?)(\\u0027|\\u002D)?i?minik>"r ?) ; #Jude 20240131
COPY:HeurACRInsSg4SgPoss ("$1"v Prop Ins Sg 4SgPoss Heur/Prop) EXCEPT ("$1"v N Gram/ACR Ins 4SgPoss Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?minik>"r) + Heur/ACR ; #Jude 20240131
Substitute:HeurACRInsSg ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Ins Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+)?)(\\u0027|\\u002D)?i?mik>"r ?) ; #Jude 20240131
COPY:HeurACRInsSg ("$1"v Prop Ins Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Ins Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?mik>"r) + Heur/ACR ; #Jude 20240131

#Heur ACR Rel
Substitute:HeurACRRelSgLu ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Rel LU Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+^i)?)(\\u0027|\\u002D)?i?llu>"r ?) - HeurOblCase - HeurExcl ; #Jude 20240206
COPY:HeurACRRelSg (Prop Rel Sg LU Heur/Prop) EXCEPT (N Gram/ACR Rel LU Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+)?)(\\u0027|\\u002D)?i?llu>"r) + Heur/ACR - HeurOblCase ; #Jude 20240206
Substitute:HeurACRRelSg ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Rel Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+^i)?)(\\u0027|\\u002D)?i?p>"r ?) - HeurOblCase - HeurExcl ; #Jude 20240206
COPY:HeurACRRelSg (Prop Rel Sg Heur/Prop) EXCEPT (N Gram/ACR Rel Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+)?)(\\u0027|\\u002D)?i?p>"r) + Heur/ACR - HeurOblCase ; #Jude 20240131
Substitute:HeurACRRelPl ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Rel Pl Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+^i)?)(\\u0027|\\u002D)?i?t>"r ?) - HeurOblCase - HeurExcl - For2Let ; #Jude 20240208
COPY:HeurACRRelPl (N Gram/ACR Abs Pl Heur/ACR) EXCEPT (N Gram/ACR Rel Pl Heur/ACR) TARGET ("<(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+)?)(\\u0027|\\u002D)?i?t>"r) + Heur/ACR - HeurOblCase ; #Jude 20240208

#Heur ACR Trm
Substitute:HeurACRTrmSg ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Trm Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?mut>"r ?) ; #Jude 20240131
COPY:HeurACRTrmSg ("$1"v Prop Trm Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Trm Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?mut>"r) + Heur/ACR ; #Jude 20240131

Substitute:HeurACRTrmPl ("\\p{Lu}+.*"r ?) ("$1"v N Gram/ACR Trm Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?nut>"r ?) ; #Jude 20240131
COPY:HeurACRTrmPl ("$1"v Prop Trm Pl Heur/Prop) EXCEPT ("$1"v N Gram/ACR Trm Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u0027|\\u002D)?i?nut>"r) + Heur/ACR ; #Jude 20240131


#Substitute:Heur/Foreign (".*"r ?) ("$1"v Heur/Foreign) TARGET ("<\(.*\)>"r ?) + Heur/Foreign ;

END

#Not ready to be in use yet as of date 20240131 - too many examples from our corpus that shouldnt be in uppercase only: e.g. ARRORSAAT

#Heur ACR Aeq

#Heur ACR Abs
Substitute:HeurACRAbsSg ("<[A-Z]+>"r ?) ("$1"v N Gram/ACR Abs Heur/ACR) TARGET ("(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+)?)i?"r ?) - HeurOblCase - HeurRel - HeurExcl ; #Jude 20240131
COPY:HeurACRAbsSg (Prop Abs Sg Heur/Prop) EXCEPT (N Gram/ACR Abs Heur/ACR) TARGET ("\([A-Z]+\)\\u0027?i?"r) + Heur/ACR - HeurOblCase - HeurRel ; #Jude 20240131

Substitute:HeurACRAbsPl ("<[A-Z]+>"r ?) ("$1"v N Gram/ACR Abs Heur/ACR) TARGET ("\([A-Z]+\)\\u0027?i?t"r ?) - HeurOblCase - HeurRel ; #Jude 20240131
COPY:HeurACRAbsPl (Prop Abs Pl Heur/Prop) EXCEPT (N Gram/ACR Abs Heur/ACR) TARGET ("\([A-Z]+\)\\u0027?i?t"r) + Heur/ACR - HeurOblCase - HeurRel ; #Jude 20240131

#Heur ACR Rel
Substitute:HeurACRRelSgLu ("[A-Z]+.*"r ?) ("$1"v N Gram/ACR Rel LU Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u002D|\\u0027)?i?llu>"r ?) ; #Jude 20240131
COPY:HeurACRRelSgLu ("$1"v Prop Rel Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Rel LU Heur/ACR) TARGET ("<\([A-Z]+\)-?'?i?llu>"r) + Heur/ACR ; #Jude 20240131
Substitute:HeurACRRelSg ("<[A-Z]+>"r ?) ("$1"v N Gram/ACR Abs Heur/ACR) TARGET ("(\\p{Lu}+(\\d+(\\p{Lu})?|\\u003A\\p{Lu}+|\\p{Ll}+)?)i?p"r ?) - HeurOblCase - HeurRel - HeurExcl ; #Jude 20240131
COPY:HeurACRRelSg (Prop Rel Sg Heur/Prop) EXCEPT (N Gram/ACR Abs Heur/ACR) TARGET ("\([A-Z]+\)\\u0027?i?p"r) + Heur/ACR - HeurOblCase - HeurRel ; #Jude 20240131

Substitute:HeurACRRelSg ("[A-Z]+.*"r ?) ("$1"v N Gram/ACR Rel Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u002D|\\u0027)?i?p>"r ?) ; #Jude 20240131
COPY:HeurACRRelSg ("$1"v Prop Rel Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Rel Heur/ACR) TARGET ("<\([A-Z]+\)-?'?i?p>"r) + Heur/ACR ; #Jude 20240131

#Heur ACR Via
Substitute:HeurACRViaSg ("[A-Z]+.*"r ?) ("$1"v N Gram/ACR Via Heur/ACR) TARGET ("<(\\p{Lu}+)(\\u002D|\\u0027)?i?kkut>"r ?) ; #Jude 20240131
COPY:HeurACRViaSg ("$1"v Prop Via Sg Heur/Prop) EXCEPT ("$1"v N Gram/ACR Via Heur/ACR) TARGET ("<\([A-Z]+\)-?'?i?kkut>"r) + Heur/ACR ; #Jude 20240131
