# -*- cg-pre-pipe: zsh -l -c '~/langtech/kal/tools/shellscripts/kal-analyse --fst' -*-

# Nutserut/Gloss kal2eng pre-pipe: zsh -l -c '~/langtech/gloss/kal2eng/kal2eng.pl --fst'

INCLUDE STATIC disambiguator.cg3 ;

# kal-pre1.cg3 er nødvendig, fordi REMCOHORT fjerner en delimiter, hvilket betyder, at der efterlades et tomt vindue. Bundlinien er, at man ikke kan fjerne delimiters og derefter bruge den i samme CG

# ============ #
# Regelsektion #
# ============ #

BEFORE-SECTIONS



#ADD misser i første omgang fordi der står en CLB mellem : og - Den fjernes før ADD
REMCOHORT:paragraftegn ("¶") + CLB IF (-*1W (":") + EOS OR (".") + EOS BARRIER EOS);
#BARRIER EOS for at sikre at det kun er den foregående sætning og ikke noget langt til venstre
#ADD:DIRSTART01 DIRTALESTART TARGET ("-") + PUNCT OR (""") + PUNCT IF (-*1W (":") + EOS BARRIER EOS);

#MERGECOHORTS for at forberede typen 100,- kroner på analysen 169,-@N> etc.
MergeCohorts ("<$1,->"v "$1,-"v Num Abs Sg) ("<(.+)>"r Num Abs Sg) WITH (1 ("<,>")) (2 ("<->")) ;

#Forsøgt flyttet fra disambiguator.cg3
#ADDCOHORT ("<->" "-" PUNCT DIRTALESTART) BEFORE @INTERJ OR @VOK (-1 BOS) (NEGATE 0 DIRTALESTART OR DIRTALEMIDDLE OR DIRTALESLUT) (NEGATE *-1W PUNCT - ("-") - (".") - KOMMA) (NEGATE 0 Adv - @INTERJ - @VOK)
#(NEGATE -*1W CLB + DIRTALEMIDDLE OR CLB + DIRTALESLUT BARRIER EOS)# -Ilami ... ASSISSAQANNGILAQ. ASUMIAA Norgep nunataata maniinnera sungiusimanngikkaa.
# ;

#20210604: Tinos bud på MERGECOHORT som en mulig vej til telefonnumre uden særlige tiltag i fst-en. Følgende virker, men vi tester først med fst

#MERGECOHORTS ("<+$1 $2 $3 $4 $5>"v "+$1 $2 $3 $4 $5"v CountryDanPhone Abs Sg) ("<+>") WITH (1 ("<(\\d{1,3})>"r Num)) (2 ("<(\\d{2})>"r Num)) (3 ("<(\\d{2})>"r Num)) (4 ("<(\\d{2})>"r Num)) (5 ("<(\\d{2})>"r Num)) ;
#MERGECOHORTS ("<$1 $2 $3 $4>"v "$1 $2 $3 $4"v DanPhone Abs Sg) ("<(\\d{2})>"r Num) WITH (1 ("<(\\d{2})>"r Num)) (2 ("<(\\d{2})>"r Num)) (3 ("<(\\d{2})>"r Num)) ;

#MERGECOHORTS ("<+$1 $2 $3 $4>"v "+$1 $2 $3 $4"v CountryKalPhone Abs Sg) ("<+>") WITH (1 ("<(\\d{1,3})>"r Num)) (2 ("<(\\d{2})>"r Num)) (3 ("<(\\d{2})>"r Num)) (4 ("<(\\d{2})>"r Num)) ;
#MERGECOHORTS ("<$1 $2 $3>"v "$1 $2 $3"v KalPhone Abs Sg) ("<(\\d{2})>"r Num Abs Sg) WITH (1 ("<(\\d{2})>"r Num Abs Sg)) (2 ("<(\\d{2})>"r Num Abs Sg)) ;

SPLITCOHORT ("<$1>"v "$1"v Num Abs Sg "<$2>"v "$2"v CLB) ("<(\\p{N}+)([.])>"r Gram/Ord) (-1 BOS) (1 ("<\\p{Lu}.*"r)) (NOT 1 MAANED) ;

#Ad hoc disambiguering, fordi kalaallisooq=SSAQ + Via + Sg + 3SgPoss blokerer for hybridsplittet
REMOVE:kalaallisooq (/"kalaallisooq"\ SSAQ\ Der/nn\ N\ Via\ Sg\ 3SgPoss/l) IF (0 (/"kalaaleq"\ iN\ iAeq\ iPl\ Gram/Hyb\ UR\ Gram/IV\ SSA/l));

SPLITCOHORT ("<$1>"v "$1"v Num Abs Sg "<$2>"v "$2"v CLB) ("<(\\p{N}{4})([.])>"r Gram/Ord) (0 EOS) ;
SPLITCOHORT ("<$1>"v "$1"v Num Abs Sg "<$2>"v "$2"v CLB) ("<(\\p{N}{4})([.])>"r Gram/Ord) (1 ("¶") + EOS) ;

#ADDCOHORT ("<.>" "." CLB) AFTER (/"[0123456789]{4}\."/r) IF (1 EOS);

#SUBSTITUTE ("<1960.>" "1960." Num Gram/Ord) ("<1960>" "1960" Num Abs Sg) TARGET ("1960.") IF (1 ("."));

# Heuristic analysis of unrecognized Prop multiwords
# If a word ends in -imi but there is an upper-case word before it, it's likely a Prop Lok
#Heur LOK
Substitute:HeurProp0001 ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)mi>"r ?) IF (-1 ("\\p{Lu}.*"r)) ; #Danske Døve Landsforbundimi qinersisoqarpoq
# Turn remaining upper-case words into Prop if there is a Prop after them
#Substitute:HeurProp0002 (?) (Prop Abs Sg Heur/Prop) TARGET ("<\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+>"r ?) IF (1* Prop BARRIER ("\\p{Ll}.*"r) OR WORD) ; #Danske Døve Landsforbundimi qinersisoqarpoq

# Specific heuristic analysis first
#Heur University of
SUBSTITUTE (?) (<university-of-> ?) TARGET ("\\p{Lu}.*"r ?) (-1 ("of") LINK -1 ("University")) ; #Jude 20240129
SUBSTITUTE (Sem/Geo Prop Abs Sg) (Sem/Geo Prop Abs Sg <university-of->) TARGET ("\\p{Lu}.*"r) (-1 ("of") LINK -1 ("University")) ; #Jude 20240129
SUBSTITUTE (Sem/Geo Prop Abs Sg) (Sem/Geo Prop Abs Sg <university-of-X->) TARGET ("\\p{Lu}.*"r) (-1 (<university-of->) LINK -1 ("of") LINK -1 ("University")) 
(NEGATE 0 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?[mi|mut]>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Rel_1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Rel Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?p\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_Rel2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Rel Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?p\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Lok1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Lok Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mi\)>"r)) ; #Jude 20240129 University of Manitobami
MergeCohorts:HeurProp_Lok2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Lok Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mi\)>"r)) ; #Jude 20240129 University of British Columbiami
MergeCohorts:HeurProp_Lok3 ("<$1=$2=$3=$4=$5$6>"v "$1=$2=$3=$4=$5"v Prop Lok Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r Sem/Geo Abs) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mi\)>"r)) ; #Jude 20240129 University of Northern British Columbiami

MergeCohorts:HeurProp_Trm1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mut\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_Trm2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r Sem/Geo Abs) - Gram/Hyb LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mut\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Abl1 ("<$1=$2=$3$4>"v "$1=$2=$3"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mit\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_Abl2 ("<$1=$2=$3=$4$5>"v "$1=$2=$3=$4"v Prop Trm Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r Sem/Geo Abs) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?mit\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_Abs ("<$1=$2=$3>"v "$1=$2=$3"v Prop Abs Sg Heur/Prop) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)>"r) - HeurOblCase) ; #Jude 20240129

MergeCohorts:HeurProp_meersoqAbs ("<$1=$2=$3$4>"v "$1=$2=$3"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Sg Heur/N) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?meersoq\)>"r)) ; #Jude 20240129
MergeCohorts:HeurProp_meersoqRel ("<$1=$2=$3$4>"v "$1=$2=$3"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Sg Heur/N) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?meersup\)>"r)) ; #Jude 20240129

MergeCohorts:HeurProp_meersoqAbsPl ("<$1=$2=$3$4>"v "$1=$2=$3"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Pl Heur/N %AmbiguøsN) ("<\(University\)>"r ?) WITH (1 ("<\(of\)>"r ?) LINK 1 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)\(-?i?meersut\)>"r)) ; #Jude 20240129
COPY:HeurProp_meersoqRelPl (N Rel) EXCEPT (N Abs) ("<University=of=.*>"r Heur/N %AmbiguøsN) ; #Jude 20240129

REMCOHORT:HeurOf ("of" ?) (-1 ("<University=of=.*>"r) + Heur/Prop OR ("<University=of=.*>"r) + Heur/N) ; #Jude 20240129
REMCOHORT:HeurProp (<university-of->) (-1 ("<University=of=.*>"r Heur/Prop)) ; #Jude 20240129
REMCOHORT:HeurProp (<university-of-X->) (-1 ("<University=of=.*>"r Heur/Prop)) ; #Jude 20240129

#Heur LOK specific and irregular
Substitute:HeurLokNunavut ("\\p{Lu}.*"r ?) ("Nunavut" Prop Lok Sg Heur/Prop) TARGET ("<Nunavumi>" ?) ; #Jude 20240129
Substitute:HeurLokNunavut ("\\p{Lu}.*"r ?) ("Nunavut" Prop Lok Sg Heur/Prop) TARGET ("<Nunavummi>" ?) ; #Jude 20240129


# Heur/Prop all cases, NOT mainly intended for input adjusted for greenlandic pronunciation: Buuarsi/Boas, Gustaat/Gustav

#Heur Abl
Substitute:HeurAblSg ("\\p{Lu}.*"r ?) ("$1"v Prop Abl Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mi?it>"r ?) ; #Jude 20240129
Substitute:HeurAbl ("\\p{Lu}.*"r ?) ("$1"v Prop Abl Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nit>"r ?) ; #Jude 20240129
COPY:HeurAblAbs ("$1n"v Abs Pl Heur/Prop) EXCEPT ("\\p{Lu}.*"r Abl Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nit>"r) + Abl + Pl + Heur/Prop 
(NEGATE 0 ("<.*ninit>"r)) ; #Jude 20240129

#Heur iAbl
Substitute:HeuriAblMeersoqAbsSg ("\\p{Lu}.*"r ?) ("$1"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Sg HeurDer Heur/N) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?meersoq>"r ?) ; #Jude 20240130
Substitute:HeuriAblMeersoqAbsPl ("\\p{Lu}.*"r ?) ("$1"v iProp iAbl iSg Gram/Hyb IR Gram/IV TUQ Der/vn N Abs Pl HeurDer Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?meersut>"r ?) ; #Jude 20240130
COPY:HeuriAblMeersoqRelSg (N Rel Pl HeurDer Heur/Prop) EXCEPT (N Abs Pl HeurDer Heur/Prop) TARGET iAbl + HeurDer + Heur/Prop ;

#Heur Aeq
Substitute:HeurAeq ("\\p{Lu}.*"r ?) ("$1"v Prop Aeq Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?[st]ut>"r ?) ; #Jude 20240129
COPY:HeurAeq (Aeq Pl Heur/Prop) EXCEPT (Aeq Sg Heur/Prop) TARGET Aeq + Heur/Prop ; #Jude 20240129

#Heur Ins
Substitute:HeurKkutIns ("\\p{Lu}.*"r ?) ("$1"v KKUT Der/nn Prop Ins Pl Heur/Prop Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)kkunnik>"r ?) ; #Jude 20240130
Substitute:HeurInsSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Ins Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?millu>"r ?) ; #Jude 20240130
Substitute:HeurInsSg ("\\p{Lu}.*"r ?) ("$1"v Prop Ins Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mik>"r ?) ; #Jude 20240129
Substitute:HeurInsPl ("\\p{Lu}.*"r ?) ("$1"v Prop Ins Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nik>"r ?) ; #Jude 20240129

#Heur  iLok
Substitute:HeuriLok ("\\p{Lu}.*"r ?) ("$1"v Sem/Geo iProp iLok iSg Gram/Hyb IP Gram/IV SIMA Der/vv V Cont 4Sg LU Heur/iLok) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?miissimallunilu>"r ?) ; #Jude 20240129

#Heur Lok
Substitute:HeurLokSg ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?[^m])-?i?mi>"r ?) ; #Jude 20240129
COPY:HeurLokAbsMi (Prop Abs Sg MI Heur/Prop) EXCEPT (Prop Lok Sg Heur/Prop) TARGET Prop + Lok + Sg + Heur/Prop ; #Jude 20240130
COPY:HeurLokRelMi (Prop Pel Sg MI Heur/Prop) EXCEPT (Prop Lok Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mmi>"r) + Prop + Lok + Sg + Heur/Prop ; #Jude 20240130
Substitute:HeurLokSgLi ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg LI Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mili>"r ?) ; #Jude 20240130
Substitute:HeurLokSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?milu>"r ?) ; #Jude 20240129
Substitute:HeurLokLuunniit ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Sg LUUNNIIT Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?miluunniit>"r ?) ; #Jude 20240129
Substitute:HeurLokPl ("\\p{Lu}.*"r ?) ("$1"v Prop Lok Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?ni>"r ?) ; #Jude 20240129
COPY:HeurLokAbs ("$1n"v Prop Abs Sg Heur/Prop) EXCEPT ("$1"v Prop Lok Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?ni>"r Prop Lok Pl Heur/Prop) ; #Jude 20240130

#Heur Rel
Substitute:HeurRelSgLi ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg LI Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?[^[m][i]])-?i?lli>"r ?) ; #Jude 20240129
Substitute:HeurRelSgLu ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?llu>"r ?) - ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?mu)-?i?llu>"r ?) ; #Jude 20240129
Substitute:HeurRelSgMi ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg MI Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?[^[m][u]])-?i?mmi>"r ?) ; #Jude 20240130
Substitute:HeurRelSg ("\\p{Lu}.*"r ?) ("$1"v Prop Rel Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?p>"r ?) ; #Jude 20240129

#Heur Trm
Substitute:HeurKkutTrm ("\\p{Lu}.*"r ?) ("$1"v KKUT Der/nn Prop Trm Pl Heur/Prop Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)kkunnut>"r ?) ; #Jude 20240130
Substitute:HeurTrmLu ("\\p{Lu}.*"r ?) ("$1"v Prop Trm Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mullu>"r ?) ; #Jude 20240129
Substitute:HeurTrmSg ("\\p{Lu}.*"r ?) ("$1"v Prop Trm Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?mut>"r ?) ; #Jude 20240129
Substitute:HeurTrmPl ("\\p{Lu}.*"r ?) ("$1"v Prop Trm Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?nut>"r ?) ; #Jude 20240129

#Heur Via
Substitute:HeurVia ("\\p{Lu}.*"r ?) ("$1"v Prop Via Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?kkut>"r ?) ; #Jude 20240129
COPY:HeurViaKkut (KKUT Der/nn Prop Abs Pl Heur/Prop) EXCEPT (Prop Via Sg Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?kkut>"r) + Via + Sg + Heur/Prop ;
Substitute:HeurVia ("\\p{Lu}.*"r ?) ("$1"v Prop Via Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?tigut>"r ?) ; #Jude 20240129

#Heur AbsRel Pl
Substitute:HeurAbsRelKkutMiu ("\\p{Lu}.*"r ?) ("$1"v Sem/Geo iProp KKUT Der/nn MIU Der/nn N Abs Pl HeurDer Heur/N) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?kkormiut>"r ?) ; #Jude 20240130
#KKUT Der/nn MIU Der/nn N Abs Pl
Substitute:HeurAbsRelMiu ("\\p{Lu}.*"r ?) ("$1"v Sem/Geo iProp MIU Der/nn N Abs Pl HeurDer Heur/N) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?miut>"r ?) ; #Jude 20240129
Substitute:HeurAbsRel ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?t>"r ?) - HeurVerb
(NEGATE 0 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)aasiit>"r ?)) 
(NEGATE 0 ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)k?kut>"r ?)) ; #Jude 20240129
#COPY:HeurAbsRel (Rel Pl Heur/Prop) EXCEPT (Abs Pl Heur/Prop) TARGET Abs + Pl + Heur/Prop ; #Jude 20240129
COPY:HeurAbsRel ("$1t"v Abs Sg Heur/Prop) EXCEPT ("$1"v Abs Pl Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?t>"r) + Abs + Pl + Heur/Prop - HeurDer ; #Jude 20240130
COPY:HeurAbsRel (Rel Pl HeurDer Heur/N) EXCEPT (Abs Pl HeurDer Heur/N) TARGET Abs + Pl + Heur/N ; #Jude 20240129

#Abs Sg
Substitute:HeurAbsAasiit ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg AASIIT Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)aasiit>"r ?) ; #Jude 20240130
Substitute:HeurAbsLi ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg LI Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?li>"r ?) - HeurOblCase - HeurRel ; #Jude 20240129
COPY:HeurAbs-Li ("$1l"v Prop Abs Sg Heur/Prop) EXCEPT ("$1"v Prop Abs Sg LI Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?li>"r ?) ; #Jude 20240129
Substitute:HeurAbsLu ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg LU Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?i?lu>"r ?) - HeurOblCase - HeurRel ; #Jude 20240129
Substitute:HeurAbsTaaq ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg TTAAQ Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)-?issaaq>"r ?) - HeurOblCase - HeurRel ; #Jude 20240130
Substitute:HeurAbsTaaq ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg TTAAQ Heur/Prop) TARGET ("<(\\p{Lu}[\\p{Ll}\\p{N}\\p{M}]+?)ttaaq>"r ?) - HeurOblCase - HeurRel ; #Jude 20240130
Substitute:HeurAbsSg ("\\p{Lu}.*"r ?) ("$1"v Prop Abs Sg Heur/Prop) TARGET ("<(\\p{Lu}+?[\\p{Ll}\\p{N}\\p{M}]+?\\p{Lu}?[\\p{Ll}\\p{N}\\p{M}]+?)-?i?>"r ?) - HeurOblCase - HeurRel - HeurVerb ; #Jude 20240129
